//──────────────────────────────────────────────────────────────────────

VipInterface PickTblFld Licensed (free) Parameters (FieldCode: string);

Interface PickTblFld 'Выбор таблицы и полей базы данных' DoAccept, EscClose, Cyan;
  show at ( 15, 10, 80 , 35);

table struct Selfld
(
  CodeTbl : word,
  CodeFld : word
)
with index
(
i0= CodeTbl+CodeFld,
i1=CodeTbl
);

Create View
var
  FieldCode : string;
  P_TabName : string;
From
  X$Files(ReadOnly),
  X$Files snX$Files,
  X$FIELDS,
//  X$FIELDS snX$FIELDS,
  Selfld
where
((
   X$Files.XF$Code == X$FIELDS.Xe$FILECODE
))
;
Parameters
  FieldCode;

QuickChoice qcFLDname
 {
  AcceptField = X$Files.XF$Name   ;
  ViewField   = X$Files.XF$Name   ;
  SearchField = X$Files.XF$Name   ;
  OnOpen  : ProcessCommand (cmInit);
  OnClose : FieldCode := X$Files.XF$Name ;
 }

Panel pan1;
  Table X$Files;
Browse  br1 (,,sci1EnEsc);
show at ( , , , 7);
Fields
  { Font = {Color =
         if(GetFirst Selfld where ((X$Files.XF$CODE == Selfld.CodeTbl)) =  tsOk, 19, 0)}};
  X$Files.XF$Name 'Идентификатор таблицы' : [17], Protect;
  X$Files.XF$TITLE 'Название таблицы'  : [18], Protect;
  X$Files.XF$Code 'Код' : [6], Protect;
end;
HandleEvent
 cmDefault :
 {
   FieldCode := X$Files.XF$Name;
 }
end;
end;
Panel pan2;
  Table X$FIELDS;
Browse  br2 (,,sci1InsPM);
show at ( , 8, ,);
Fields
  { Font = {Color =
         if(GetFirst Selfld where ((X$Fields.XE$FILECODE == Selfld.CodeTbl
                                    and X$FIELDS.XE$CODE == Selfld.CodeFld)) =  tsOk, 19, 0)}};
         if(GetFirst Selfld where ((X$Fields.XE$FILECODE == Selfld.CodeTbl
                                    and X$FIELDS.XE$CODE == Selfld.CodeFld)) =  tsOk,'√', ''):[1], skip;
  X$FIELDS.Xe$Name  'Идентификатор поля' : [20], Protect;
  X$FIELDS.Xe$TITLE 'Заголовок поля'     : [20], Protect;
  if(X$Fields.Xe$DataType=ftString,'String',
    if(X$Fields.Xe$DataType=ftDouble,'Double',
      if(X$Fields.Xe$DataType=ftDate,'Date',
        if(X$Fields.Xe$DataType=ftTime,'Time',
          if(X$Fields.Xe$DataType=ftChar,'Char',
            if(X$Fields.Xe$DataType=ftByte,'Byte',
              if(X$Fields.Xe$DataType=ftWord,'Word',
                if(X$Fields.Xe$DataType=ftInteger,'Integer',
                  if(X$Fields.Xe$DataType=ftLongint,'LongInt',
                    if(X$Fields.Xe$DataType=ftDateTime,'DateTime',
                      if(X$Fields.Xe$DataType=ftBoolean,'Boolean',
                        if(X$Fields.Xe$DataType=ftComp,'Comp','<тип>')))))))))))) 'Тип поля' : [10], Protect;
end;
HandleEvent
cmDefault:
{
  FieldCode:= '';
  if GetFirst Selfld = tsOk
  {
    _Loop Selfld
    {
      if GetFirst X$FIELDS where((Selfld.CodeTbl ==X$Fields.XE$FILECODE
                              and Selfld.CodeFld == X$Fields.XE$CODE))= tsOk
      {
        if getFirst snX$Files where ((Selfld.CodeTbl == snX$Files.XF$Code)) = tsOk
          if FieldCode = ''
          {
            if Upcase(P_TabName) <> snX$Files.XF$Name
              FieldCode := snX$Files.XF$Name+'.'+X$FIELDS.Xe$Name;
            else
              FieldCode := '.'+X$FIELDS.Xe$Name;
          }
          else
            FieldCode := FieldCode +', '+snX$Files.XF$Name+'.'+X$FIELDS.Xe$Name;
      }
    }
  }
  else
  {
    if CurFormatInArea(br2)= br2
    {
      if Upcase(P_TabName) <> X$Files.XF$Name
        FieldCode := X$Files.XF$Name+'.'+X$FIELDS.Xe$Name;
      else
        FieldCode := '.'+X$FIELDS.Xe$Name;
    }
    else
    {
      FieldCode := X$Files.XF$Name;
    }
  }
  if Instr('.',FieldCode)> 1
    FieldCode:= ' '+FieldCode;
}

cmMarkUnMark:
{
  if(GetFirst Selfld where ((X$Fields.XE$FILECODE == Selfld.CodeTbl
                             and X$FIELDS.XE$CODE == Selfld.CodeFld)) =  tsOk)
    Delete current Selfld;
  else
  {
    Selfld.CodeTbl := X$Fields.XE$FILECODE;
    Selfld.CodeFld := X$FIELDS.XE$CODE;
    insert current Selfld;
  }

  if (getnext X$FIELDS)=tsOk {};
    Rereadrecord(#X$FIELDS);
}
cmSelectAll:
{
  delete all from Selfld;
  StartNewVisual(vtRotateVisual, vfTimer+vfBreak, ''#3'Выбор ...'#13#3, 1);
  _try
  {
    pushpos(#X$FIELDS);
    _loop X$FIELDS
    {
      Selfld.CodeTbl := X$Fields.XE$FILECODE;
      Selfld.CodeFld := X$FIELDS.XE$CODE;
      insert current Selfld;
      NextVisual;
    }
  } // try
  _except
   on ExUserBreak :
   {
     delete all from Selfld;
   }
   _finally
   {
     poppos(#X$FIELDS);

     ReReadRecord(#X$FIELDS);
     StopVisual('',0);
   }
   Rereadrecord(#X$FIELDS);
}

cmUnSelectAll:
{
  delete all from Selfld;
  Rereadrecord(#X$FIELDS);
}

end;
end;
HandleEvent
cmInit:
{
  delete all from Selfld;
  P_TabName := '';
  if FieldCode <> ''
    if GetFirst X$Files where (( FieldCode == X$Files.XF$Name)) = tsOk
    {
      P_TabName := FieldCode;
    }
  FieldCode:='';
  cfsSetProp('PickTblFld','centered',true);
}
cmDone:
{
}


End;
End.
