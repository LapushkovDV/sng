#include PerAcces.VIH

#component "Z_StaffOrders"

alter interface RPD_3;

#include OWFunc.vpp
#declare OverworkCalculationF2(_Tbl)
  if(#_Tbl.OWMode = ocmDaily, 'поденный',
    if(#_Tbl.OWMode = ocmSummed, 'суммированный',
      if(#_Tbl.OWMode = ocmNone, 'несуммированный', '')))
#end

overload
  procedure IRPD_Base.EnableDisable;
  procedure DepartmentAndMen;
end;

var VPerAccess: SetPersonsAccess;

create view
as select
  #OverworkCalculationF2(NewAppoint)
  (fieldname = OverworkCalculation2)
;

OverloadFormats scrDet3;

screen scrDet3 (,hcStaffWinPr, sci1Esc);
show at (, 5,,);
table NewAppoint;
  fields
    ContDoc2.wAttrDoc2 ('Основное назначение или совмещение'):
      [list 'основное назначение', 'совмещение'], protect;
    fullNamePodr: protect;
    CatPost.Name: protect;
    KlRejim.NRejim ('График (назначение)'): protect;
    Appointments.TaxRate ('Оклад (назначение)') : [11.3], protect;

    [SNG_scdLastUser0] vTSFuncs.GetRealCorrCoeff(Appointments.Tariff, Appointments.LastUser)
        ('Корректирующий коэффициент (назначение)', ): [10.3], protect;

    //новое назначение
    ContDoc.Dat1 ('Замещает с указанной даты',
             , sci13esc): [10,'DD/MM/YYYY'], {Font = {BackColor = if(ContDoc.Dat1 = ZeroDate, ColorNeed, 0)}}, noprotect;
    ContDoc.Dat2 ('Замещает по указанную дату',
             , sci13esc): [10,'DD/MM/YYYY'], noprotect;
    NewAppoint.wAddWord1 ('Замещает сотрудника или вакантную ставку'):
      [list 'сотрудника ', 'вакантную ставку'], protect;
    NewAppoint.sWorkPlace: protect, pickbutton;
    ChangePersons.FIO ('ФИО замещаемого сотрудника',,
           sci13esc): {Font = {BackColor = if(not IsValid(#ChangePersons), ColorNeed, 0)}}, protect;
    ContDoc2.wDopAttr ('Основное назначение или совмещение'):
      [list 'основное назначение', 'совмещение'], protect;
    [SNG_fTabN]
      GetStrTabN(ChangeAppoint.AppTabNmb, ChangeAppoint.StrTabN): skip;
    OldFullNamePodr: protect;
    NewPostCat.Name: protect;

    fNotSignal: noprotect;

    NewKlSisOpl.NaSisOpl ('Система оплаты труда. ',
             , sci13esc):  Protect, noDel ;
    NewAppHist.TaxRate ('Оклад, ставка, тариф', ) : [11.3], noprotect;
    AppKlVal.SimvolV ('Валюта', , sci13esc), protect, noDel;

    NewAppHist.coef1 ('Количество занимаемых ставок'): [5.3], noprotect;
    [SNG_scdLastUser1] vTSFuncs.GetRealCorrCoeff(NewAppoint.Tariff, NewAppoint.LastUser)
              ('Корректирующий коэффициент'): [10.3], noprotect;
    FullIf ('Источники финансирования',, sciStaffTypical13Esc): protect;
    NewWorkCondCat.Name ('Условия труда.',
             hcStaffAppWorkCond, sci13esc) : Protect;
    fRejimStrSec ('Режим труда и продолжительность рабочего дня (недели), ч',
             , sci13esc) : Protect;
    OverworkCalculation2  ('Способ учета сверхурочных работ',,sciStaffTypical13Esc): protect;
    NewAppoint.PeriodOW ('Период расчета сверхурочных работ при суммированном учете',,): [list 'месяц', 'квартал', 'полугодие', 'год'], protect;
    NewDopInfoCat.Name ('Дополнительные сведения',, sci13esc): protect;
    NewTypeMove.Name ('Причина замещения',
             , sci13esc) : Protect;
    fAgreement ('Доп. соглашение к трудовому договору'): protect, pickbutton;
    sExternalLink ('Идентификатор доп.соглашения',, sciStaffTypical1Esc): protect, {hyperlink = (sExternalLink <> '')};
    ContDoc.SBottom ('Основание замещения'): [40], noprotect;
    buttons
      cmValue14,[singleline],,,
       'Составляющие корректирующего коэффициента';

      cmValue5, [singleline];
      cmValue6, [singleline];

      cmValue12,[singleline],,,
       'Составляющие корректирующего коэффициента';
      cmValue25, [SingleLine],,, 'Штрих-код';
      cmValue1,,,'Доплаты, надбавки', ;
      cmViewAdvance1,,,'Просмотр примечания к приказу';
      cmValue21,,, 'Дополнительные условия по приказу';
      cmValue3,,,'Проконтролировать';
<<
`Вид назначения` .@@@@@@@@@@@@@@@@@@@@@@
 Основная ставка .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 График          .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Оклад.@@@@@@@@@@@@       Корр.коэф..@@@@@@@<.>>.>

 Замещает  с     .@@@@@@@@@@@@ по .@@@@@@@@@@@@ .@@@@@@@@@@@@@@@@@@@@@@@ Раб. место .@@@@@@@@@@@@
`Cотрудника     `.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
`Вид назначения `.@@@@@@@@@@@@@@@@@@@@@@       `Табельный номер`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Замещ.ставка    .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

 [.] Не отправлять сигнал об изменении л/с в рабочую корзину`
 С-ма оплаты     .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@     <.Подставить данные по основному назначению.>
 Оклад           .@@@@@@@@@@@@@@@.@@@@@@@@@@@@@     <.     Подставить данные по замещению      .>
 Кол-во ставок   .@@@@@ Корр.коэф..@@@@@@@<.>>.> `ИФ`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

 Условия труда   .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  Режим труда.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
`Учет сверхурочных`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@   `Период` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
                                               Доп. сведения.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Причина         .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
`Доп. соглашение`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ `Ссылка на ДС в Directum`.@@@@@@@@@@@@@
                                                                                  <. Штрих-код .>
 Основание       .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
    <.  Доплаты  .>      <. Примечание .>      <. Доп. условия .>      <. Проконтролировать.>
>>
end; // screen scrDet3

procedure DepartmentAndMen;
{
  inherited::DepartmentAndMen;

  if NewAppoint.wAddWord1 > 0
  { // ставку
    SetFieldAndLabelVisible(#SNG_fTabN, false);
  }
  else
  { //сотрудника
    SetFieldAndLabelVisible(#SNG_fTabN, true);
  }
}

procedure IRPD_Base.EnableDisable;
{
  inherited::EnableDisable;

  if ((VPerAccess.GetAccessMask(6) and 32768) = 0)
  {
    SetFieldAndLabelVisible(#Appointments.TaxRate, false);
    SetFieldAndLabelVisible(#NewAppHist.TaxRate, false);
    cfsSetProp('T_SCRDET3_Оклад', cfpVisible, false);
    cfsSetProp('T_SCRDET3_Оклад#1', cfpVisible, false);
  }

  SetFieldAndLabelVisible(#SNG_fTabN, true);

  if getfirst ContDoc2 = tsOk
    if getfirst NewAppoint = tsOk
      if NewAppoint.wAddWord1 > 0
      { // ставку
        SetFieldAndLabelVisible(#SNG_fTabN, false);
      }

  if (not isMultiCorrCoef)
  {
    if not isCorrCoef
    {
      ClearFieldOption(#SNG_scdLastUser0, sfVisible);
      ClearFieldOption(#SNG_scdLastUser1, sfVisible);
    }
    else
    {
      SetFieldOption(#SNG_scdLastUser0, sfVisible);
      SetFieldOption(#SNG_scdLastUser1, sfVisible);

      if not funIsOrderConfirm
      {
        SetFieldOption(#SNG_scdLastUser0, ofSelectable);
        SetFieldOption(#SNG_scdLastUser1, ofSelectable);
      }
      else
      {
        ClearFieldOption(#SNG_scdLastUser0, ofSelectable);
        ClearFieldOption(#SNG_scdLastUser1, ofSelectable);
      }
    }
  }
  else
  {
    ClearFieldOption(#SNG_scdLastUser0, sfVisible);
    ClearFieldOption(#SNG_scdLastUser1, sfVisible);
  }
}

HandleEvent

cmOnVipLoad:
{
  inherited::handleEvent(cmOnVipLoad);
  IsSummedOWH := false;
}

cmPick:
{
  if CurField = #OverworkCalculation2
  {
    case runmenu('CaseOWModeSNG3') of //Выбор для способа учета сверхурочных
      cmValue61:
        set NewAppoint.OWMode := ocmDaily;
      cmValue62:
        set NewAppoint.OWMode := ocmSummed;
      cmValue63:
        set NewAppoint.OWMode := ocmNone;
    end;  //case

    SetModified(true);
    UpdateTable;
    #SetVisibleOWPeriod(NewAppoint, #NewAppoint.PeriodOW)
  }
  else
  {
    inherited::handleevent(cmPick);
  }
}

end; //HandleEvent

end.

CaseOWModeSNG3 menu   //Выбор для поля способ учета сверхурочных
{
  - 'Поденный',cmValue61,'Способ учета сверхурочных поденный';
  - 'Суммированный',cmValue62,'Способ учета сверхурочных суммированный';
  - 'Несуммированный',cmValue63,'Способ учета сверхурочных несуммированный';
}
