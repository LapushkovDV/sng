/*************************************************************************************************\
* Наименование: Фильтр по системному каталогу (кадры)                                             *
* Контур/Модуль: *                                                                                *
* Примечание:                                                                                     *
*                                                                                                 *
* Вид работы  |Номер         |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
* Разработка  |              |09/03/17|Кузьмин П.Ю.             |НПО Энергомаш                    *
\*************************************************************************************************/

#include Pickcat.vih
#include CatFunc.vih           // Функции работы с Catalogs

#include GP_UserFilterCatalogs.vih
interface GP_UserFilterCatalogs;

var iCatFunc:CatalogsFunctions;   // Переменная типа интерфейс CatalogsFunctions
    SysCode:integer;
    cNode:comp;

function GetFilterStr:string;
var MarkerCount:longint;
    cRec:comp;
{
  MarkerCount:=GetMarkerCount(mFilter);
  if (MarkerCount=0) result:='Не установлен'
  else if (MarkerCount<>1) result:='Множественный выбор ('+string(MarkerCount)+')'
  else {
    GetMarker(mFilter,0,cRec);
    if (cRec=0) {
      ClearMarker(mFilter);
      result:=GetFilterStr();
    }
    else result:=iCatFunc.GetCatalogsName(cRec);
  };
}

constructor UserFilterCatalogs(NoField:longint;SaveName:string;SysCode:integer;cNode:comp=0);
{
  private::SysCode:=SysCode;
  private::cNode:=cNode;

  result:=inherited::UserFilter(NoField,SaveName);
}

procedure Pick;
var cRec:comp;
{
  if (SysCode=0) exit;

  GetMarker(mFilter,0,cRec);
  DoneMarker(mFilter,'CatalogsMarker');
  if (cNode=0)
    RunInterface(PickCatalog2,SysCode,cRec,100);
  else {//Выбор элементов конкретного узла
    SaveMyDsk(cRec, 'PickCatalog2_DopTblCurPos');
    cRec:=cNode;
    RunInterface(PickCatalog2,SysCode,cRec,107);
  }
  mFilter:=InitMarker('CatalogsMarker',8,0,1);

  private::FilterStr:=GetFilterStr;
}

end.
