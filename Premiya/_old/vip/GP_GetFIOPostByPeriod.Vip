/*************************************************************************************************\
* Наименование:  Выбор ЗГД                                                                       *
* Контур/Модуль: Кадры                                                                            *
* Примечание:                                                                                     *
*                                                                                                 *
* Вид работы  |Номер         |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
* Разработка  |#816          |09/01/19|Кириллов Э.П.            |НПО Энергомаш                    *
* Разработка  |#3876         |09/01/19|Кириллов Э.П.            |НПО Энергомаш                    *
\*************************************************************************************************/

#include GP_GetFIOPostByPeriod.Vih
#doc
Выбор ЗГД
<p>#816 Закрепление за должностями
#end

//******************************************
Interface GP_GetFIOPostByPeriod 'Выбор ЗГД' , DoAccept, EscClose,Cyan;
//******************************************
show at (,,80,20);
var
   wIntMode   :word;  //cgiPick - тип выбора
  SPostRazdel:string;//Наименование подраздела
  DTb:date;  // Дата начала периода
  DTE:date;  // Дата окончания периода
  pLink_App   :comp; //Возвращаемый APoointments.Nrec
  pLink_Pers  :comp; //Возвращаемый Persons.Nrec
  pLink_Staff :comp; //Возвращаемый StaffStruct.Nrec
//================================
  pCatPostMain:comp;//Catalogs.SysCode=-22;
//================================
Table Struct local tmp_pers
( Dept_Kod    :string[30]   "Код подр."//	Код подразделения ЗГД, из ставки ШР
 ,Post        :string       "Должность ЗГД" //	Должность из ставки ШР
 ,AppointDate :date         "Дата открытия ставки"//	Дата открытия ставки
 ,DisMissDate  :date         "Дата закрытия ставки" //	Дата закрытия ставки
 ,Fio         :string[100] "ФИО"  //	ФИО сотрудника, занимающего ставку
 ,cPersons    :comp
 ,cApp        :comp
 ,cStaff      :comp
)
with index
( tmp_pers01 = Fio
 ,tmp_pers02 = Dept_Kod
 ,tmp_pers03 = Post
 ,tmp_pers04 = AppointDate
);
//================================
Create view
as select CatMain.Nrec,tmp_pers.*
From
   Catalogs CatMain
 , tmp_pers(tmp_pers01)
where
(( //  -22
   49 == CatMain.SysCode

));

//******************************************
Parameters
   wIntMode
#doc
   cgiPick - тип выбора
#end
 , SPostRazdel
#doc
 Наименование подраздела
#end
 , DTb
#doc
Дата начала периода
#end
 , DTE
#doc
Дата окончания периода
#end
 , pLink_App
#doc
Возвращаемый APoointments.Nrec
#end
 , pLink_Pers
#doc
Возвращаемый Persons.Nrec
#end
 , pLink_Staff
#doc
Возвращаемый StaffStruct.Nrec
#end
;

//********************************************
Browse brPres (,,sci1Esc);
Table tmp_pers;
Fields
 tmp_pers.Fio          'ФИО'           ('ФИО сотрудника, занимающего ставку'):[25],Protect,NoDel;
 tmp_pers.Dept_Kod     'Код подр.'     ('Код подразделения ЗГД, из ставки ШР'):[10],Protect,NoDel;
 tmp_pers.Post         'Должность ЗГД' ('Должность из ставки ШР' ):[25],Protect,NoDel;
 tmp_pers.AppointDate  'Дата назначения','на должность'  ('Дата назначения на должность'):[10],Protect,NoDel;
 tmp_pers.DisMissDate   'Дата ухода','с должности'  ('Дата ухода с должности'):[10],Protect,NoDel;
end; //Browse brPres (,,sci1Esc);

HandleEvent
cmInit:
{ timelog_init;
  pCatPostMain:=0;
  _Loop CatMain
  { pCatPostMain:=CatMain.Nrec;
  }
  Timelog('GP_GetFIOPostByPeriod->cmInit:pCatPostMain='+string(pCatPostMain,0,0));
  Timelog('GP_GetFIOPostByPeriod->cmInit:SPostRazdel='+SPostRazdel);
  delete all tmp_pers;
  _try
  { SQL select Distinct
      CatDept.Code as Dept_Kod //   :string[30]   "Код подр."//	Код подразделения ЗГД, из ставки ШР
     ,CatPost.Name as Post     //   :string       "Должность ЗГД" //	Должность из ставки ШР
     ,Appointments.AppointDate  as AppointDate //:date         "Дата открытия ставки"//	Дата открытия ставки
     ,Appointments.DisMissDate  as DisMissDate //:date         "Дата закрытия ставки" //	Дата закрытия ставки
     ,Persons.Fio               as Fio //      :string[100] "ФИО"  //	ФИО сотрудника, занимающего ставку
     ,Persons.Nrec              as cPersons  //   :comp
     ,Appointments.Nrec         as cApp  //      :comp
     ,Appointments.StaffStr     as cStaff//cStaff      :comp
    From Catalogs CatPostMain
    inner join Catalogs CatPost      on CatPost.cParent   = CatPostMain.Nrec
    inner join Appointments on
          Appointments.Post  = CatPost.Nrec
      and Appointments.LPrizn>=0
      and Appointments.LPrizn<=3
      and Appointments.AppointDate <= CASE WHEN :(DTE)=#Date(0,0,0) THEN #Date(1,1,2100)  ELSe :(DTE) END
      and (Appointments.DisMissDate =#Date(0,0,0) or Appointments.DisMissDate>=:(DTB) )
    Inner join Persons on Persons.Nrec =Appointments.Person
    Inner join Catalogs CatDept on CatDept.Nrec = Appointments.Department
    where
          //    CatPostMain.MainLink= :(pCatPostMain)
            CatPostMain.cParent = :(pCatPostMain) //сбита подцепка, выбираем из уровня "Должности"
        and CatPostMain.Lpr     = 0
        and CatPostMain.Name    = :(SPostRazdel)
    into tmp_pers byName;
  }
  _Except
  else {
     timelog('ошибка заполнения tmp_pers ExploreException)');
  }
  ReScanPanel(#tmp_pers);
}
cmDefault:
{ DTb         :=tmp_pers.AppointDate ; // Дата начала периода
  DTE         :=tmp_pers.DisMissDate  ; // Дата окончания периода
  pLink_App   :=tmp_pers.cApp        ; //Возвращаемый APoointments.Nrec
  pLink_Pers  :=tmp_pers.cPersons    ; //Возвращаемый Persons.Nrec
  pLink_Staff :=tmp_pers.cStaff      ; //Возвращаемый StaffStruct.Nrec
}
end;
end.
