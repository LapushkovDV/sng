/*************************************************************************************************\
* Наименование: Выбор группы документов                                                           *
* Контур/Модуль: *                                                                                *
* Примечание:                                                                                     *
*                                                                                                 *
* Вид работы  |Номер задачи  |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
* Разработка  |#195          |18/09/17|Кузьмин П.Ю.             |НПО Энергомаш                    *
\*************************************************************************************************/
#include marker.vih // описание объекта и объявление интерфейса
                    // marker c реализацией по умолчанию
#include GP_PickTypeDocs.Vih

interface GP_PickTypeDocs 'Выбор группы документов', Cyan, DoAccept, EscClose;
show(20,5,105,25);
var wIntMode:word;
var TypeDocName:string;
var pTypeDoc:comp;
var wTypeDoc:word;
var pMarker:IMarker(Marker) new;
create view
from
  GP_TypeDocs(TYPEDOCS03)
Bounds byVed 100<<=Gp_TypeDocs.wType(noIndex) and 200>>=Gp_TypeDocs.wType(noIndex) ordered by TableOrder
//------------
Bounds byKPI 200>>Gp_TypeDocs.wType(noIndex)

Bounds bySCUD 1000<<=Gp_TypeDocs.wType(noIndex) and 1100>>=Gp_TypeDocs.wType(noIndex) ordered by TableOrder
Bounds byIIKO 3000<<=Gp_TypeDocs.wType(noIndex) and 3100>>=Gp_TypeDocs.wType(noIndex) ordered by TableOrder
Bounds byLOTTERY 4000<<=Gp_TypeDocs.wType(noIndex) and 4100>>=Gp_TypeDocs.wType(noIndex) ordered by TableOrder
;
parameters wIntMode,TypeDocName,pTypeDoc,wTypeDoc;

browse brTypeDocs (,,sci1Esc);
table GP_TypeDocs;
recMarker=pMarker;
fields
  GP_TypeDocs.Name 'Наименование' ('Наименование',,):[40],Protect;
  GP_TypeDocs.wType 'Код' ('Код документа',,):[40],Protect;
end;

HandleEvent
cmInit:{
  if wTypeDoc>=100 and wTypeDoc<=200
    AddBounds(tbByVed)
  else
  if wTypeDoc=1000
    AddBounds(tbBySCUD)
  else
  if wTypeDoc=3000
    AddBounds(tbByIIKO)
  else
  if wTypeDoc=4000
    AddBounds(tbByLOTTERY)
  else
    AddBounds(tbByKPI);

  if (wIntMode and cgiPickMult)=cgiPickMult
  {     pMarker.AutoSave := True;
        pMarker.LoadAuto := True;
        pMarker.StoreName := 'GP_PickTypeDocs';
        pMarker.Caption   := (' Отмечено [%d] ');
  }
  else  pMarker.Disable:=true;

  if (TypeDocName<>'')
    if (getfirst GP_TypeDocs where((TypeDocName==GP_TypeDocs.Name))=tsok) {};
  ReScanpanel(#Gp_TypeDocs);
}
cmDefault:{
  TypeDocName:=GP_TypeDocs.Name;
  pTypeDoc   :=GP_TypeDocs.Nrec;
  wTypeDoc   :=Gp_TypeDocs.wType;
  if (wIntMode and cgiPickMult)=cgiPickMult
  { if PMarker.Count=0
      pMarker.Mark;
    pMarker.ExportToName(pMarker.StoreName);
  }

}
end;
end.
