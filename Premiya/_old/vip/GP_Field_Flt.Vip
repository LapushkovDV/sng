/*************************************************************************************************\
* Наименование:  Выбор значений полей                                                             *
* Контур/Модуль: Кадры                                                                            *
* Примечание:    следует заполнить tmp_flt_Field                                                  *
*                КПИ. Спецификация Премий                                                         *
* Вид работы  |Номер         |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
* Разработка  |#195          |21/09/17|Кириллов Э.П.            |НПО Энергомаш                    *
\*************************************************************************************************/

#include GP_Field_Flt.Vih

//-------------------------------------

interface GP_Field_Flt 'Выбор значений' , DoAccept;
Show at (5,5,65,20);
var
   wIntMode:word;
   cRec:comp;
   sTitleKod:string;
   sTitleName:string;
   Marker_vib :tptr;
create view as select tmp_flt_field.*
 , if(FoundMarker(Marker_Vib,tmp_flt_field.nrec),chr(251),'') (FieldName=Picked_Flt)

from tmp_flt_field(tmp_flt_field05);
Parameters
  wIntMode,cRec,Marker_vib,sTitleKod,sTitleName
  ;

//*************************************************8
QuickChoice qcByCode
{
  AcceptField = tmp_flt_field.Nrec;
  ViewField = tmp_flt_field.sCode;
  ViewField = tmp_flt_field.sName;
  // только текстовый поля!
  SearchField = tmp_flt_field.sCode;
  OnOpen : {
  }
  OnClose : {
    cRec := tmp_flt_field.Nrec;
    InsertMarker(Marker_Vib, tmp_flt_field.NRec);
  }
} //QuickChoice qcByCode
QuickChoice qcByName
{
  AcceptField = tmp_flt_field.Nrec;
  ViewField = tmp_flt_field.sName;
  ViewField = tmp_flt_field.sCode;
  likeField = tmp_flt_field.sName;
  likeField = tmp_flt_field.sCode;
  // только текстовый поля!
  SearchField = tmp_flt_field.sName;
  OnOpen : {
  }
  OnClose : {
    cRec := tmp_flt_field.Nrec;
    InsertMarker(Marker_Vib, tmp_flt_field.NRec);
  }
}//QuickChoice qcByName



Function ShowIHeadFlt:boolean;
{ Result:=true;

} //Function ShowIHeadFlt:boolean;

Browse brwnGetFilter (,,sci1EscIns);
  Table tmp_flt_field ;
  Fields
  {Font = {Color = if(Picked_Flt<>'', ColorMark, 0); }};
    Picked_Flt    :[1],Protect;
    tmp_flt_field.sName 'Наименование':[40],Protect;
    tmp_flt_field.sCode 'Код'         :[20],Protect;
  end;
  HandleEvent
  cmInit:
  { if marker_vib=0
    { Message('Ошибка вызова интерфейса выбора значений');
      Abort; exit;
    }
  }
  cmMarkUnMark :
  {
    if (not isValid(#tmp_flt_field) )
    { Exit; }

    InvertMarker(Marker_Vib, tmp_flt_field.NRec);
    RedrawCurrentAndGo(GetCurrentFormat, True);
    ShowIHeadFlt;
  }
  cmSelectAll :
  { if (not isValid(tntmp_flt_field) )
    { Exit; }
    ClearMarker(Marker_Vib);
    PutCommand(cmInvertAll);
  }
  cmInvertAll :
  {
    if (not isValid(tntmp_flt_field) )
    { Exit; }
    StartNewVisual(vtNumericVisual, vfTimer + vfBreak + vfConfirm, 'Идет пометка записей ', 1);
    PushPos(tntmp_flt_field);
    _Loop tmp_flt_field
    { InvertMarker(Marker_Vib, tmp_flt_field.NRec);
      if (Not NextVisual) Break;
    }
    PopPos(tntmp_flt_field);
    ReScanPanel(tntmp_flt_field);
    StopVisual('', 0);
    ShowIHeadflt;
  }
  cmUnSelectAll :
  {
    ClearMarker(Marker_Vib);
    ReScanPanel(tntmp_flt_field);
    ShowIHeadFlt;

  }
  cmDefault:
  {
    If GetMarkerCount(Marker_Vib)=0
      InsertMarker(Marker_Vib, tmp_flt_field.NRec);
  }
  end;


end.
