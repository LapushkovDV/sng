/* Контур/Модуль: Утилиты                                                                          *
* Примечание:                                          *
* Вид работы  |Номер         |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
* Разработка  |#175          |07/09/17|Кириллов Э.П.            |НПО Энергомаш                    *
\*************************************************************************************************/


/*
!Именительный 	Номинатив 	Кто? Что? (есть) 	--- --- 	-а, -я 	---, -о, -е 	--- 	-ы, -и, -а, -я
!Родительный 	Генитив 	Кого? Чего? (нет) 	без, у, до, от, с, около, из, возле, после, для, вокруг 	-ы, -и 	-а, -я 	-и 	---, -ов, -ев, -ей
!Дательный 	Датив 	Кому? Чему? (дам) 	к, по 	-е, -и 	-у, -ю 	-и 	-ам, -ям
!Винительный 	Аккузатив 	Кого? Что? (вижу) 	в, за, на, про, через 	-у, -ю 	-о, -е 	--- 	---, -ы, -и, -а, -я, -ей
!Творительный 	Инструментатив 	Кем? Чем? (горжусь) 	за, над, под, перед, с 	-ой (-ою), -ей (-ею) 	-ом, -ем 	-ю 	-ами, -ями
!Предложный 	Препозитив 	О ком? О чем? (думаю) 	в, на, о, об, обо, при 	-е, -и 	-е, -и 	-и 	-ах, -ях
*/
/*
http://morpher.ru/russian/Adjective.aspx
=========================
Падеж          Единственное                         Множественное
               Мужской         Средний    Женский
Именительный    -oй,-ый,-ий    -ое -ее    -ая -яя    -ые -ие
Родительный    -oго                       -oй  -ей   -ых -их
Дательный      -oму                       -oй         -ым -им
Винительный     =И/Р                      -ую -юю    =И/Р
Творительный   -ым -им                    -oй|-oю    -ыми -ими
Предложный     -oм                        -oй    -ых -их
=========================
http://polyidioms.narod.ru/index/sklonenie_prilagatelnykh/0-216
*/
#include odecl.vih
#include GP_ODecl.Vih
#DEFINE NO_ODECL_LOG_
//---------------------------------
Interface GP_ODecl ;
//---------------------------------
// склонение
create view vodecl
var w_fio:string;
    wc_FIOExceptions:comp;
    ww_Fio_Prev :string;
    wc_Abit : Comp;
    odecl_NCase: word;
    _Type :word; // 0 - абитуриенты, 4 - склонение фио
as select persons.fio
from persons, Catalogs
#IFDEF _SPB_
, U_Student
, U_Abiturient
, U_AAboutFio
, CasePsn
#ENDIF
where ((
  'С' == persons.isemployee
 and w_fio==persons.fio
));
var
 odecl : declension ; // склонение фио
 odecl_i:longint ;
 logfile_odecl: string;


//=======================================
Procedure Odecl_logwrite(const w:string); 
//=======================================
{ #__UNUSED__(w)
#IFDEF _ODECL_LOG_
logstrtofile(logfile_odecl,w)
#ENDIF
}


//=======================================
Function My_Sclonenie(odecl_w :string ;odecl_NCase: word; cRec : comp=0 ): string ;
//=======================================
var 
  odecl_w1,odecl_w2,odecl_w3,odecl_w3_def:string ;
  wkl_check_first : boolean ;
  length_obr :longint;
  wTable: word;
{
//1 именительный
//2 родительный
//3 дательный  - кому чему
//+10  до первого существительного
//+20  фио работника/студента
//+30  фио абитуриента
//+40  CasePsn

  odecl_w:=replace(odecl_w,'  ',' ');
#IFDEF _ODECL_LOG_
   Odecl_logwrite('My_Sclonenie Падеж '+string(odecl_NCase)+', строка='+odecl_w);
#END
  odecl_w1:='';
  odecl_i:=0;
  odecl_w2:='';
  if odecl_NCase>20
  { if pos('.',odecl_w)>0
     odecl_NCase:=odecl_NCase-20;
  }
  if odecl_NCase>20 //ФИО
  { wTable:=odecl_NCase div 10;
    odecl_NCase:=odecl_NCase mod 10 ;
    
    vodecl.w_fio:=odecl_w;
    //if wTable=2
    if cRec=0
    { if  wTable=2
      { if vodecl.getfirst persons=0 
        { cRec   :=vodecl.Persons.NRec;
          odecl_w:=vodecl.Persons.Fio
        }
      }
    } //if cRec=0
    if cRec>0
    {  { Result:=Odecl.FIODeclension(cRec, odecl_w, odecl_NCase);
         if Trim(REsult)<>''
         {  Exit;
         }
       }
    }
    My_Sclonenie:=Odecl.FIODeclension(0, odecl_w, odecl_NCase);
    exit;
  } 
  if odecl_NCase>10
  { odecl_NCase:=odecl_NCase mod 10 ;
    Odecl_logwrite(' ищем первое существительное');
    wkl_check_first:=true ;
  } 
  else
  { wkl_check_first:=false ;
  } //Усть-Лабинский
  length_obr:=0;
  do {
    odecl_i:=odecl_i+1;
    if odecl_i>20 then break ;
    odecl_w3:=ExtractDelimitedWord(odecl_w,odecl_i,' ');
    length_obr:=length_obr+length(odecl_w3)+1;
    Odecl_logwrite('итерация '+string(odecl_i)+', подстрока='+odecl_w3);
    if pos('.',odecl_w3)>0 then
    { odecl_w1:=odecl_w1+' '+odecl_w3;
      continue;
    }
    if pos('-',odecl_w3)>0
    { odecl_w3_def:=substr(odecl_w3,1,pos('-',odecl_w3));
      odecl_w3    :=substr(odecl_w3,pos('-',odecl_w3)+1,200);
      Odecl_logwrite(' odecl_w3_def='+odecl_w3_def);
    }
    else
    { odecl_w3_def:='';
    } 
    odecl_w3:=trim(odecl_w3); 
    odecl_w2:='';
    if 
    //substr(odecl_w3,length(odecl_w3)-1,2)<>'ла'    and 
    substr(odecl_w3,length(odecl_w3)-1,2)<>'ра'
    //and substr(odecl_w3,length(odecl_w3)-1,2)<>'ой'
    //and substr(odecl_w3,length(odecl_w3)-1,2)<>'ии' 
    and substr(odecl_w3,length(odecl_w3)-2,3)<>'ого'
    and substr(odecl_w3,length(odecl_w3)-1,2)<>'вы'
    and substr(odecl_w3,length(odecl_w3)-1,2)<>'га'
    and substr(odecl_w3,2,2)<>upcase(substr(odecl_w3,2,2)) // исключаем абревиатуру
    {  //http://polyidioms.narod.ru/index/sklonenie_prilagatelnykh/0-216
       if substr(odecl_w3,length(odecl_w3)-1,2)='ое'
       { case odecl_NCase of 
          2: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ого';
          3: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ому';
          4:{} //=1
          5: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ым';
          6: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ом';
          end;
       }
       else
       if substr(odecl_w3,length(odecl_w3)-1,2)='ые'
       { case odecl_NCase of 
          2: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ых';
          3: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ым';
          4:{} //=1
          5: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ыми';
          6: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ых';
          end;
       }
       else
       if substr(odecl_w3,length(odecl_w3)-1,2)='ее'
       { case odecl_NCase of 
          2: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'его';
          3: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ему';
          4:{} //=1
          5: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'им';
          6: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ем';
          end;
       }
       else
       if substr(odecl_w3,length(odecl_w3)-1,2)='ие'
       { // если прилагательное
         /* нн в конце что ли поискать :(, но летние
         case odecl_NCase of 
          2: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'их';
          3: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'им';
          4:{} //=1
          5: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ими';
          6: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'их';
          end;
         */ 
         case odecl_NCase of 
          2: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ия';
          3: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ию';
          4:{} //=1
          5: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ем';
          6: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ии';
          end;
       }
       else
       if substr(odecl_w3,length(odecl_w3)-2,3)='шая'
       { case odecl_NCase of
          2: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ей';
          3: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ей';
          4: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ую';
          5: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ей';
          6: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-2)+'ой';
          end;
       }
       else
        odecl_w2:= odecl.SurNameDeclension(odecl_w3,0,odecl_NCase,1);
      if odecl_w2=odecl_w3
      { if substr(odecl_w3,length(odecl_w3),1)='ь'
        { case odecl_NCase of 
          2: odecl_w2:= substr(odecl_w3,1,length(odecl_w3)-1)+'и'
          end;
        }
      }  
      Odecl_logwrite(' склоненное='+odecl_w2);
    }
    else
      odecl_w2:=odecl_w3;
    odecl_w1:=odecl_w1+odecl_w3_def+odecl_w2+' ';
    Odecl_logwrite(' строка ='+odecl_w1);
    if odecl_i<=2  and wkl_check_first
    { 
      if (pos(locase(substr(odecl_w3,length(odecl_w3),1)),'аеийояыюэ') = 0 //АЕИЙОЯЫЮЭ
      and odecl_w3<>'школа' //and substr(odecl_w3,length(odecl_w3)-1,2)<>'ла'  
      ) 
      or (  // substr(odecl_w3,length(odecl_w3)-1,2)='ла'  
           //and 
         substr(odecl_w3,length(odecl_w3)-1,2)='ря'  // утеря
         )
      { //odecl_w1:=odecl_w1+substr(odecl_w,length(odecl_w3)+2,100) ;
        odecl_w1:=odecl_w1+substr(odecl_w,length_obr+1,100) ;
        Odecl_logwrite(' нашли существительное='+odecl_w1);
        break ;
      }  
    }
  } while odecl_w2<>''
  odecl_w1:=replace(odecl_w1,'щом','щем');
  My_Sclonenie:=trim(odecl_w1)
}


//==================================
// Привести к виду Хyyyyyyyyyy Xyyyyyyy Xyyyyyyyyy
//==================================
Function FIOFirstChairUp(ww_St : string):string;
var
 ww_ResSt : String ;
 ww_i     : Byte ;
 ww_Ch    : Char ;
 ww_k     : Byte ;
{ Result:=ww_ST;
 if vodecl.ww_Fio_Prev =Result then Exit;
 ww_St    := Trim(ww_St) ;
 ww_ResSt := '' ;
 ww_K := 0;
 for (ww_i := 1 ; ww_i <= Length(ww_St); ww_i++)
  {
   //if (SubStr(St, i, 1) <> ' ')
   //необходимо, чтобы двойные фамилии (которые  пишутся через дефис) все его части начинались с большой буквы "Прончатов-Рубцов."
    if ww_St[ww_i]<>' ' and ww_St[ww_i]<>'-'
    {
     ww_K++;
     ww_Ch := ww_St[ww_i];
     if ( ww_K = 1 )
       ww_Ch:= Char(UpCase(ww_Ch));

     ww_ResSt := ww_ResSt + ww_Ch ;
    }
    else
    {
      ww_K := 0;
      if (ww_i > 1)
      if (ww_Ch <> ' ')// убираем 2 пробела!
      {
        ww_Ch := ' ' ;
        ww_ResSt := ww_ResSt + ww_St[ww_i];//' ' ;
      }
    }
  } // for

   if vodecl.wc_FIOExceptions>0 // getfirst ParentCatalog where ((cg_FIOExceptions == ParentCatalog.SysCode)) = tsOk
   {
     var tempStr, part : string='';

     ww_k := 0;
     tempStr := ww_ResSt;
     ww_ResSt := '';
     if pos(' ',tempStr) > 0
     do //while (Pos(' ', tempStr) > 0)
     { ww_k:=Pos(' ', tempStr);
       if ww_k>0
         part := substr(tempstr,1,ww_k )
       else  
         part:=tempstr;
       if vodecl.getfirst Catalogs where ((0 ==Catalogs.Lpr and wc_FIOExceptions == Catalogs.MainLink and Trim(Locase(part))== Catalogs.Name))= tsOk
       {
          ww_ResSt := ww_ResSt + Locase(part);
       }
       else
       {    ww_ResSt := ww_ResSt + part;
       }
       ww_k:=Pos(' ', tempStr);
       if ww_k=0 then break;
       tempStr := substr(tempstr,ww_k + 1, Length(tempStr));
     } while true //(Pos(' ', tempStr) > 0)
  }
  Result := ww_ResSt ;
  
  vodecl.ww_Fio_Prev :=Result;
} //Function FIOFirstChairUp(ww : string):string;



#IFDEF _ODECL_LOG_
HandleEvent
cmOnVipLoad:
{ logfile_odecl:=GetStringParameter('Files','OutputFilesDirectory',0)
           +'\!odecl.log'
  if fileexist(logfile_odecl) deletefile(logfile_odecl)
  vodecl.wc_FIOExceptions:=0 //3347 Исключения из фио   
  if vodecl.getfirst Catalogs where ((cg_FIOExceptions == Catalogs.SysCode)) = tsOk
  { vodecl.wc_FIOExceptions:=vodecl.Catalogs.Nrec;
  }
  if (vodecl.getfirst Catalogs where ((0 ==Catalogs.Lpr and wc_FIOExceptions == Catalogs.MainLink)) )<>tsok
  {  vodecl.wc_FIOExceptions:=0;
  }
  

}
end;
#END
end.
