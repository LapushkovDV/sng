/*************************************************************************************************\
* Наименование: Регистрация дополнительных пунктов меню                                           *
* Контур/Модуль: *                                                                                *
* Примечание:                                                                                     *
*                                                                                                 *
* Вид работы  |Номер         |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
* Разработка  |              |29/03/17|Кузьмин П.Ю.             |НПО Энергомаш                    *
* Разработка  |#3727         |07/12/18|Кириллов Э.П.            |НПО Энергомаш                    *
\*************************************************************************************************/

#include GP_MenuDynamic.vih
interface GP_MenuDynamic 'Регистрация дополнительных пунктов меню' Gray;
show(20,8,80,15);

var IsClearResources:boolean;
create view
from
  X$RESOURCES
where((
  3==X$RESOURCES.XR$TYPE
));

screen ScRepParam(,,sci1Esc);
fields
  'Программа добавляет новые пункты в меню',skip,{Font={bold=true}};
  IsClearResources ('Принудительно удаляет записи с меню из X$RESOURCES',,):NoProtect;
buttons
  cmAddMenu,default,,'Добавить',,;
  cmDelMenu,,,'Удалить',,;
  cmCancel,,,'Выход',,;
<<

  .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

    [.] Удалить все меню из системной таблицы`

 <. Добавить .>     <. Удалить .>             <. ~О~тмена  .>
>>
end;

exception ExStoreMenuHowDynamics;

procedure ClearResources;
{
  if (delete X$RESOURCES <> tsok)
    message('Ошибка удаления записи в таблице X$RESOURCES!',error);
}

HandleEvent
cmInit:{
  timelog_init;
  if (not pr_CurUserAdmin) {
    message('Программа доступна только пользователям с правами администратора.',error);
    abort;
    exit;
  }
}
cmAddMenu:{
  StartNewVisual(vtRotateVisual, vfTimer+vfBreak+vfConfirm,'Добавление новых пунктов в меню...', 0);

  if (IsClearResources) ClearResources;

  timelog('GP_MenuDynamic->cmAddMenu');
  var MenuID:longint=0;
  _try {
    //Меню модуля "Настройка"
    MenuID:=LoadMenuEx('C_COMMON::Nastr_Admin',false,true);

    //=====================================
    //Добавление пунктов меню модуля "Настройка - администрирование - Объединение ЭМ"
    //=====================================
    AddMenuItem(MenuID,'separator',1);
    AddMenuItemEx2(MenuID,'Регистрация дополнительных пунктов меню',
                          'Регистрация дополнительных пунктов меню','',cmRunInterface,0,'GP_Common::GP_MenuDynamic');

    //=====================================
    if (not StoreMenuHowDynamics(MenuID,'C_COMMON::Nastr_Admin'))
      _raise ExStoreMenuHowDynamics;

    timelog('GP_MenuDynamic->cmAddMenu->C_COMMON::Nastr_Admin');
    GP_MenuDynamic_AddItems;

    ReinitHeaderMenu;

    Message('Дополнительные пункты меню добавлены');
    timelog('GP_MenuDynamic->cmAddMenu->C_COMMON::Nastr_Admin');
  }
  _except
    on ExStoreMenuHowDynamics:
      message('Ошибка выполнения StoreMenuHowDynamics',error);
    on ExVip:
      message(ExploreException,error);
  _finally {
    if (MenuID<>0) DisposeLoadMenu(MenuID);
  }

  StopVisual;
}
cmDelMenu:{
  StartNewVisual(vtRotateVisual, vfTimer+vfBreak+vfConfirm,'Удаление новых пунктов в меню...', 0);

  if (IsClearResources) ClearResources;

  _try {
    //Меню модуля "Настройка"
    DeleteMenuHowDynamics('C_Common::Nastr_Admin');

    GP_MenuDynamic_DelItems;

    ReinitHeaderMenu;

    Message('Дополнительные пункты меню удалены')
  }
  _except
    on ExVip:
      message(ExploreException,error);

  StopVisual;
}
end;

end.
