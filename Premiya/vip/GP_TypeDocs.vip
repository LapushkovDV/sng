/* Контур/Модуль: *                                                                                *
* Примечание:                                                                                     *
*                                                                                                 *
* Вид работы  |Номер задачи  |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
* Разработка  |#195          |24/08/17|Кузьмин П.Ю.             |НПО Энергомаш                    *
* Разработка  |#175          |23/10/17|Кириллов Э.П             |НПО Энергомаш                    *
\*************************************************************************************************/

#include GP_StatusAccessEdit.vih
#include GP_TypeDocs.vih

interface GP_TypeDocs;

create view ltTypeDocs
as select GP_TypeDocs.Nrec
from
  GP_TypeDocs,GP_Status
where ((
 GP_TypeDocs.NRec == GP_Status.cTypeDoc
))
  ;

procedure AddStatus(Code,Name:string;IsDef:boolean=false);
{
  ClearBuffer(tnGP_Status);
  GP_Status.cTypeDoc:=GP_TypeDocs.NRec;
  GP_Status.Code:=Code;
  GP_Status.Name:=Name;
  GP_Status.IsDefault:=IsDef;
  if (insert current GP_Status <> tsok)
    _raise ExDataBase;
}

procedure InitNewTypeDoc;
{
  _try {
    case GP_TypeDocs.wType of
    GP_iSC_Events_From_Scud:
    { //GP_iSC_Events_From_Scud
      AddStatus('01','СКУД');
      AddStatus('02','СКУД, корректировка');
      AddStatus('03','Добавлен вручную',True);
      AddStatus('04','Исключено из учета');
    }
     GP_iSC_statement_on_absence: //   = 1010;//'Заявление на отсутствие';
    {
      AddStatus('01','оформляемый');
      AddStatus('02','согласован');
      AddStatus('03','утвержден',True);
      AddStatus('04','отменен');

    }
     GP_iSC_exception_of_periods //   = 1011;//'Исключение периодов из нарушений';
    ,GP_iSC_OrderOver            //    = 1014;//Приказ на сверхурочные
    ,GP_iSC_OrderOver2           //   = 1015;//Приказ на оплату переработки
    ,GP_iSC_OrderHoliday         //   = 1016;//Приказ на работу в выходной день
    :
    {
      AddStatus('01','оформляемый');
      AddStatus('02','исполняемый');
      AddStatus('03','утвержден',True);
      AddStatus('04','отменен');

    }
    else
    {
      AddStatus('01','оформляемый',true);
      AddStatus('02','исполняемый');
      AddStatus('03','закрытый');
    }
    end;
  }
  _except
  on ExDataBase:{
    message('Ошибка вставки записи в таблицу GP_Status!',error);
  }
}

function AddTypeDoc(TypeName:string;_wType:word=0;_wTable:word=0):comp;
{
  result:=0;
  if (Trim(TypeName)='') exit;

  ClearBuffer(tnGP_TypeDocs);
  GP_TypeDocs.Name:=TypeName;
  GP_TypeDocs.wType:=_wType;
  GP_TypeDocs.wTable:=_wTable;
  if (insert current GP_TypeDocs <> tsok)
    message('Ошибка вставки записи в таблицу GP_TypeDocs!',error)
  else {
    result:=GP_TypeDocs.NRec;
    InitNewTypeDoc;
  }
}

function GetTypeDocByName(TypeName:string;_wType:word=0;_wTable:word=0):comp;
{
  if (getfirst fastfirstrow GP_TypeDocs where((TypeName==GP_TypeDocs.Name))=tsok)
  { result:=GP_TypeDocs.Nrec;
    if _wTable<>0
    { if GP_TypeDocs.wTable<>_wTable
      { update current GP_TypeDocs set GP_TypeDocs.wTable:=_wTable;
      }
    }
    if _wType<>0
    { if GP_TypeDocs.wType<>_wType
      { update current GP_TypeDocs set GP_TypeDocs.wType:=_wType;
      }
    }
  }
  else
    result:=AddTypeDoc(TypeName,_wType,_wTable);
}

end.
