/*************************************************************************************************\
* Наименование: Реализация класса GP_RoleGroups "Группы ролей пользователей"                      *
* Контур/Модуль: *                                                                                *
* Примечание:                                                                                     *
*                                                                                                 *
* Вид работы  |Номер задачи  |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
* Разработка  |#195          |23/08/17|Кузьмин П.Ю.             |НПО Энергомаш                    *
\*************************************************************************************************/

#include GP_RoleGroups.vih
interface GP_RoleGroups;

create view ltRoleGroups
var
 sGroupName:string;
as select GP_RoleGroups.Nrec
from
  GP_RoleGroups,GP_Roles,GP_UserRoles;

procedure InitNewGroup;
{
  ClearBuffer(tnGP_Roles);
  GP_Roles.cRoleGroup:=GP_RoleGroups.NRec;
  GP_Roles.Name:='Администратор';
  GP_Roles.IsAdmin:=true;
  if (insert current GP_Roles <> tsok)
    message('Ошибка вставки записи в таблицу GP_Roles!',error)
  else {
    ClearBuffer(tnGP_UserRoles);
    GP_UserRoles.cRoleGroup:=GP_RoleGroups.NRec;
    GP_UserRoles.cRole:=GP_Roles.NRec;
    GP_UserRoles.cUser:=UserId;
    if (insert current GP_UserRoles <> tsok)
      message('Ошибка вставки записи в таблицу GP_UserRoles!',error);
  }
}

function AddGroup(GroupName:string):comp;
{
  result:=0;
  if (Trim(GroupName)='') exit;

  ClearBuffer(tnGP_RoleGroups);
  GP_RoleGroups.Name:=GroupName;
  if (insert current GP_RoleGroups <> tsok)
    message('Ошибка вставки записи в таблицу GP_RoleGroups!',error)
  else {
    result:=GP_RoleGroups.NRec;
    InitNewGroup;
  }
}

function GetGroupByName(GroupName:string):comp;
{
  if (getfirst fastfirstrow GP_RoleGroups where((GroupName==GP_RoleGroups.Name))=tsok)
    result:=GP_RoleGroups.Nrec
  else
    result:=AddGroup(GroupName);
}

end.
