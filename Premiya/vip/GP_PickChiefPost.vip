/*************************************************************************************************\
* Наименование: Выбор должности ответственного                                                    *
* Контур/Модуль: Кадры                                                                            *
* Примечание:                                                                                     *
*                                                                                                 *
* Вид работы  |Номер         |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
* Разработка  |#195          |22/09/17|Кузьмин П.Ю.             |НПО Энергомаш                    *
\*************************************************************************************************/
#include GP_PickChiefPost.vih
interface GP_PickChiefPost 'Выбор должности ответственного' Cyan, DoAccept;
show(10,5,100,30);

var cExecutor,cChiefStaffStr:comp;
    PostName:string;
    DPeriod:date;
table struct MemCat(NRec:comp,Code:string,Name:string,DepName:string)
with index(i1=NRec,i2=Name);

create view
from
  MemCat(i2);

parameters cExecutor,cChiefStaffStr,PostName,DPeriod;

//------------------------------------
function Fill_post(_cExecutor:comp;_DPeriod:date):boolean;
{ Result:=false;
  cExecutor:=_cExecutor;
  DPeriod  :=_DPeriod;
  delete all MemCat;
  if _DPeriod=Zerodate  //Лукьянова Елена Викторовна
  { sql
    select distinct GP_KPI_Chief.cStaffStr,Post.Code,Post.Name,Dep.Name
    from
      GP_KPI_Chief
      inner join Catalogs Post on (GP_KPI_Chief.cPostChief=Post.NRec)
      left join Catalogs Dep on (GP_KPI_Chief.cPodrChief=Dep.NRec)
    where
      :(cExecutor)=GP_KPI_Chief.cExecutor
    into MemCat;
  }
  else
  { sql
    select distinct GP_KPI_Chief.cStaffStr,Post.Code,Post.Name,Dep.Name
    from
      GP_KPI_Chief
      inner join Catalogs Post on (GP_KPI_Chief.cPostChief=Post.NRec)
      inner join GP_KPI_Plan  on GP_KPI_Plan.cChiefKPI = GP_KPI_Chief.NRec
             and ( GP_KPI_PLAN.DEND=#Date(0,0,0) or GP_KPI_PLAN.DEND>=:(DPeriod) )
      left join Catalogs Dep on (GP_KPI_Chief.cPodrChief=Dep.NRec)
    where
      :(cExecutor)=GP_KPI_Chief.cExecutor
    and ( GP_KPI_CHIEF.DEND =#Date(0,0,0)  or GP_KPI_PLAN.DEND>=:(DPeriod))
    into MemCat;
  }
  Result:=(RecordsInTable(tnMemCat)<>0);
} //function Fill_post(_cExecutor:comp;_DPeriod:date):boolean;

//------------------------------------
function FindFirstPost(var _name:string):comp;
{ Result:=0;
  _name:='';
  if getfirst MemCat<>tsok exit;
  Result:=MemCat.NRec;
  _name:=MemCat.Name;
}
//------------------------------------
function FindNextPost(var _name:string):comp;
{ Result:=0;
  _name:='';
  if getnext MemCat<>tsok exit;
  Result:=MemCat.NRec;
  _name:=MemCat.Name;
}


//------------------------------------
browse brMemCat (,,sci1Esc);
table MemCat;
fields
  MemCat.Code 'Код', 'должности' ('Код должности по штатному расписанию'): [10], Protect;
  MemCat.Name'Должность' ('Должность по штатному расписанию',, ): [30], Protect;
  MemCat.DepName 'Наименование СЕ ШР':[30],Protect;
end;

HandleEvent
cmInit:{
  Fill_post(cExecutor,DPeriod);
  if (RecordsInTable(tnMemCat)=0) {
    message('Не найдено ни одной должности ответственного.');
    abort;
    exit;
  }

  if (cChiefStaffStr<>0)
    if (getfirst MemCat where((cChiefStaffStr==MemCat.NRec))=tsok) {};
}
cmDefault:{
  cChiefStaffStr:=MemCat.NRec;
  PostName:=MemCat.Name;
}
end;
end.
