/*************************************************************************************************\
* Наименование: Фильтр по статусу                                                                 *
* Контур/Модуль: *                                                                                *
* Примечание:                                                                                     *
*                                                                                                 *
* Вид работы  |Номер         |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
* Разработка  |#1024         |11/05/18|Кузьмин П.Ю.             |НПО Энергомаш                    *
\*************************************************************************************************/

#include GP_UserFilterNotes.vih

interface GP_UserFilterNotes;

var VidDoc:word;
    cTemplate: comp;

create view
select
  KatNotes.NRec
from
  KatNotes;

function GetFilterStr:string;
var kol:longint;
    cRec:comp;
{
  kol:=GetMarkerCount(mFilter);
  if (kol=0)
    result:=''
  else
    if (kol<>1) result:='Множественный выбор ('+string(kol)+')'
  else {
    GetMarker(mFilter,0,cRec);
    if (cRec=0) {
      ClearMarker(mFilter);
      result:=GetFilterStr();
    }
    else if (getfirst fastfirstrow KatNotes where((cRec==KatNotes.NRec))=tsok)
      result:=KatNotes.Name
    else
      result:='?!! '+string(cRec,0,0);
  }
}

constructor UserFilterNotes(NoField:longint;SaveName:string;VidDoc:word;cTemplate:comp = 0);
{
  private::VidDoc:=VidDoc;
  private::cTemplate:=cTemplate;

  result:=inherited::UserFilter(NoField,SaveName);
}

procedure Pick;
var cRec:comp;
{
  if cTemplate <> 0
    RunInterface(GP_GetNormDocNotes, 0, VidDoc, cTemplate, mFilter);
  else
  {
    StoreMarker(mFilter,'MKatNotes');
    GetMarker(mFilter,0,cRec);
    if (RunInterface('GetSomKatNotes', VidDoc, word(0), word(0), true, cRec)=cmDefault) {
      DoneMarker(mFilter,'');
      mFilter:=InitMarker('MKatNotes',8,0,1);
    }
  }

  private::FilterStr:=GetFilterStr;
}

end.
