/*************************************************************************************************\
* Наименование: Реализация класса GP_Status "Статусы документов"                                  *
* Контур/Модуль: *                                                                                *
* Примечание:                                                                                     *
*                                                                                                 *
* Вид работы  |Номер         |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
* Разработка  |#195          |14/09/17|Кузьмин П.Ю.             |НПО Энергомаш                    *
\*************************************************************************************************/

#include GP_TypeDocs.vih

#include GP_Status.vih

interface GP_Status;

var TypeDocs:GP_TypeDocs;
    cCurTypeDoc:comp;
create view ltStatus
as select GP_Status.Nrec
from
  GP_Status,
  GP_StatusRoles,
  GP_StatusChange,GP_StatusChangeRoles;

constructor Init;
{
  result:=true;

  cCurTypeDoc:=0;
}

constructor InitForTypeDoc(TypeDocName:string);
{
  result:=false;

  cCurTypeDoc:=TypeDocs.GetTypeDocByName(TypeDocName);
  result:=cCurTypeDoc<>0;
}

function DelRec(cStatus:comp):boolean;
var Rez:integer;
{
  result:=false;

  //Начало транзакции
  Rez:=BeginConcurrentTransaction(trNoLock);
  if (Rez<>tsok)
    message('Ошибка открытия транзакции - '+string(Rez),error);
  else {
    _loop GP_StatusChange where((cStatus==GP_StatusChange.cStatusFrom)) {
      Rez:=delete GP_StatusChangeRoles where((GP_StatusChange.NRec==GP_StatusChangeRoles.cStatusChange));
      if (Rez<>tsOK) {
        message('Ошибка удаления записи в таблице GP_StatusChangeRoles - '+string(Rez),error);
        break;
      }
    }

    if (Rez=tsok) {
      Rez:=delete GP_StatusChange where((cStatus==GP_StatusChange.cStatusFrom));
      if (Rez<>tsOK)
        message('Ошибка удаления записи в таблице GP_StatusChange - '+string(Rez),error);
    }

    if (Rez=tsok)
      _loop GP_StatusChange where((cStatus==GP_StatusChange.cStatusTo)) {
        Rez:=delete GP_StatusChangeRoles where((GP_StatusChange.NRec==GP_StatusChangeRoles.cStatusChange));
        if (Rez<>tsOK) {
          message('Ошибка удаления записи в таблице GP_StatusChangeRoles - '+string(Rez),error);
          break;
        }
      }

    if (Rez=tsok) {
      Rez:=delete GP_StatusChange where((cStatus==GP_StatusChange.cStatusTo));
      if (Rez<>tsOK)
        message('Ошибка удаления записи в таблице GP_StatusChange - '+string(Rez),error);
    }

    if (Rez=tsok) {
      Rez:=delete GP_StatusRoles where((cStatus==GP_StatusRoles.cStatus));
      if (Rez<>tsOK)
        message('Ошибка удаления записи в таблице GP_StatusRoles - '+string(Rez),error);
    }

    if (Rez=tsok) {
      Rez:=delete GP_Status where((cStatus==GP_Status.NRec));
      if (Rez<>tsOK)
        message('Ошибка удаления записи в таблице GP_Status - '+string(Rez),error);
    }

    if (Rez=tsok) {//Запись в БД
      Rez:=EndTransaction;
      if (Rez<>tsOK)
        message('Ошибка завершения транзакции - '+string(Rez),error);
      result:=Rez=tsok;
    } else {//Откат
      Rez:=AbortTransaction;
      if (Rez<>tsOK)
        message('Ошибка отката транзакции - '+string(Rez),error);
    }
  }
}

function SetDefault(cStatus:comp):boolean;
var Rez:integer;
{
  result:=false;

  //Начало транзакции
  Rez:=BeginConcurrentTransaction(trNoLock);
  if (Rez<>tsok)
    message('Ошибка открытия транзакции - '+string(Rez),error);
  else {
    Rez:=update GP_Status
         where((cCurTypeDoc==GP_Status.cTypeDoc and true==GP_Status.IsDefault(noindex))) set
           GP_Status.IsDefault:=false;
    if (Rez<>tsOK)
      message('Ошибка модификации записи в таблице GP_Status - '+string(Rez),error);

    if (Rez=tsok) {
      Rez:=update GP_Status where((cStatus==GP_Status.NRec)) set GP_Status.IsDefault:=true;
      if (Rez<>tsOK)
        message('Ошибка модификации записи в таблице GP_Status - '+string(Rez),error);
    }

    if (Rez=tsok) {//Запись в БД
      Rez:=EndTransaction;
      if (Rez<>tsOK)
        message('Ошибка завершения транзакции - '+string(Rez),error);
      result:=Rez=tsok;
    } else {//Откат
      Rez:=AbortTransaction;
      if (Rez<>tsOK)
        message('Ошибка отката транзакции - '+string(Rez),error);
    }
  }
}

function GetDefault:comp;
{
  result:=0;

  if (getfirst fastfirstrow GP_Status
      where((cCurTypeDoc==GP_Status.cTypeDoc and true==GP_Status.IsDefault(noindex)))=tsok)
    result:=GP_Status.NRec;
}

function GetByName(StatusName:string):comp;
{
  result:=0;

  if (getfirst fastfirstrow GP_Status
      where((cCurTypeDoc==GP_Status.cTypeDoc and StatusName==GP_Status.Name(noindex)))=tsok)
    result:=GP_Status.NRec;
}

function GetNextCode:string;
{
  if ((getlast GP_Status where((cCurTypeDoc==GP_Status.cTypeDoc)) ordered by GP_Status.Code)=tsok)
    result:=NextNumStr(GP_Status.Code)
  else
    result:='01';
}

property TypeDoc:comp absolute cCurTypeDoc read write;

end.
