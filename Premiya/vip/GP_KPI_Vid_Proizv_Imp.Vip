/*************************************************************************************************\
* Наименование: импорт в БД внешнего атрибута "Вид производства" к структурным подразделениям ШР  *
* Контур/Модуль: Кадры                                                                            *
* Примечание: ВА привязывать ко всем подразделениям, у которых совпадает первые 3 цифры кода.     *
*                                                                                                 *
* Вид работы  |Номер         |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
* Разработка  |#1493         |19/12/17|Кириллов Э.П.            |НПО Энергомаш                    *
\*************************************************************************************************/
#include GP_Status.vih
#include GP_KPI_RaiseEdit.Vih


VipInterface GP_KPI_Vid_Proizv_Imp Licensed(free);
#doc
импорт в БД внешнего атрибута "Вид производства" к структурным подразделениям ШР
#end
Interface GP_KPI_Vid_Proizv_Imp 'импорт в БД внешнего атрибута "Вид производства" к структурным подразделениям ШР ' , EscClose;
//---------------------------------------
//----------------------------------
var
  nm_file:string;
  logfilenm:string;
  logfilekl:boolean;
  VA_VID_PROIZV:comp;
  cat_main_M11:comp;
//----------------------------------
Create view
var
  Podr_kod:string;
as select Catalogs.Nrec
From  Catalogs
where ((
     cat_main_M11 == Catalogs.MainLink
and  0            == Catalogs.Lpr
and  Podr_kod <<= Catalogs.Code
and  Podr_kod+'ЯЯЯЯЯ' >>= Catalogs.Code
));
//------------------------------------
Create view vSpKau
var
  pSpKau:comp;
as select SpKau.name
From SpKau
where ((
   pSpKau == Spkau.nrec
)); //Create view vSpKau


#include Excel_Imp.Vpp


#include ShowKau.Vpp
//------------------------------------------------
// Получить значение аналитики
//------------------------------------------------
Function GetAnaliticName(_KodGroup:word;_cKau:comp):string; Cacheable;
{ Result:='';
  if _KodGroup>10000
  { vSpkau.pSpKau:=_cKau;
    if vSpKau.getfirst spkau=tsok
      Result:=vSpKau.Spkau.name;
  }
  else
  { Result:=#ShowKau(_KodGroup,_cKau);
  }
} //Function GetAnaliticName(_KodGroup:word;_cKau:comp):string; Cacheable;

//------------------------------------------------
// Получить значение аналитики
//------------------------------------------------
Function GetAnaliticNrec(_KodGroup:word;_sKau:string):comp; Cacheable;
{ Result:=0;
  if _KodGroup>10000
  {
    if vSpKau.getfirst spkau where (( _KodGroup == spKau.KodGrKau and _sKau== SpKau.code)) =tsok
      Result:=vSpKau.Spkau.nrec;
  }
  else
  { Result:=0;
  }
} //Function GetAnaliticName(_KodGroup:word;_cKau:comp):string; Cacheable;




Function FormDoc:boolean;
{ Result:=False;
  logfilenm:=GetStringParameter('Files','OutputFilesDirectory',0);
  if not FileExist(nm_file)
  { message('Не нашли файл '
     +chr(13)+nm_file );
    Exit;
  }
  cat_main_M11:=0;
  if getfirst Catalogs where (( -11 == Catalogs.syscode ))=tsok
  { var _nrec:comp=catalogs.nrec;
    cat_main_M11:=_nrec;
  }
  if cat_main_M11=0
  { Message('Не нашли точку входа для -11 каталога(СЕШР)' );
    Exit;
  }
  VA_VID_PROIZV:=ExtAttr_ID(coCatalogs,'Вид производства');

  //------------------------------
  logfilekl:=true;
  //------------------------------
  var isAllOk:boolean=true;
  //====================================
  v_Imp.num_col:=60;// иначе первые 50
  Timelog('GP_KPI_DOC_Vid_Proizv_Imp->ReadDataFromExcel:Start');
  XlKillExcel;
  if not ReadDataFromExcel(0,nm_file,'Лист1',False,true)
  { message('Ошибка импорта из'+chr(13)+nm_file);
      exit ;
  }
  Timelog('GP_KPI_DOC_Vid_Proizv_Imp->ReadDataFromExcel:Finish');
  //====================================
  var wexrow_:longint;
  wexrow_:=0;
  var wkl_err:boolean;
  wkl_err:=false;
  Timelog('GP_KPI_DOC_Vid_Proizv_Imp->Import:Start');
  XlKillExcel;
  v_imp._loop tmp_excel
  { if not nextvisual then
    { isAllOk:=false;
        break;
    }
    wexrow_++ ;
    if wexrow_=1
    { continue;
    }
    var _sKau:string=v_imp.tmp_excel.dmcell[2];
    Podr_kod:=SubStr(LpadCh(v_imp.tmp_excel.dmcell[1],'0',3),1,3);
    var _cSpkau:comp=GetAnaliticNrec(10025,_sKau);
    if _cSpkau>0
    { _Loop catalogs
      { coSetAttrID(coCatalogs,Catalogs.Nrec,VA_VID_PROIZV, _cSpkau,_sKau)
      }
    }
  } //v_imp._loop tmp_excel
  Timelog('GP_KPI_DOC_Vid_Proizv_Imp->Import:Finish');
  ReScanPanel(#Catalogs);
  Result:=true;
} //FomdDoc





Screen scrParam;
Table Catalogs;
Fields
  nm_file:NoProtect, PickButton;
Buttons
  cmOk    ,,, 'Установить фильтр', hcAllBOk, scGalDial;
  cmCancel,,, 'Отмена', hcAllBCancel, scGalDial;

<<
  Файл   .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 <. Продолжить .>  <.   Отмена   .>
>>
end;


HandleEvent
  cmInit:
  { nm_file:='';
    if not ReadMyDsk(nm_file,'GP_KPI_DOC_Vid_Proizv_Imp_nm_file',False)
      nm_file:='d:\Galaxy\RedMine\_GP\KPI\Sverka\Подразделения_по_видам_производств_уточненная_от_Козлова_Л.П..xlsx' ;
    logfilenm:=GetStringParameter('Files','OutputFilesDirectory',0)
                               +'!TimeLog.log';
    timelog_init;
  }
  cmDone:
  {

  }
  cmOk:
  { SaveMyDsk(nm_file,'GP_KPI_DOC_Vid_Proizv_Imp_nm_file');

    StartNewVisual(vtNumericVisual, vfTimer, 'Ждите, идет сбор данных', 0);
    FormDoc;
    StopVisual('',0);
  }
  cmCheckField:
  { case curField of
    #nm_file:
    { if not FileExist(nm_file) message('Файл не найден!');

    }
    end;
  }
  cmPick:
  { case CurField of
    #nm_file:
    { var ww_str:string=GetFileName('*.xlsx','Выберите файл процентами премий из КИС');
      if ww_str<>'' set nm_file:=ww_str;
    }
    end;
  }
  end;

end.
