/*************************************************************************************************\
* Наименование: Импорт сумм фондов из КИС                                                         *
* Контур/Модуль: Кадры                                                                            *
* Примечание:                                                                                     *
*                                                                                                 *
* Вид работы  |Номер         |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
* Разработка  |#195          |03/12/17|Кириллов Э.П.            |НПО Энергомаш                    *
\*************************************************************************************************/
#include GP_Status.vih



VipInterface GP_KPI_Fond_Import Licensed(free);
#doc
Импорт сумм фондов из КИС
#end
Interface GP_KPI_Fond_Import 'Импорт сумм фондов из КИС' , EscClose;
//---------------------------------------
Function ChkErr(_podr_kod:string;_sTypeVed:string) : boolean; forward;
//----------------------------------
Table Struct tmp_Fond
(  cDoc01   : comp
  ,cDoc02   : comp
  ,cDoc01SP : comp
  ,cDoc02SP : comp
  ,DocNumber01:string
  ,DocNumber02:string
  ,wMonth:word "Месяц"
  ,wYear :word "Год"
  ,sTypeVed:string "Тип ведомости"
  ,Description:string "Примечание"
  ,Podr_Kod:string "Подр."
  ,Podr_name:string "Наименование подр."
  ,Su01 :double "Экономия за результаты подр."
  ,Su02 :double "Экономия фонд руководителя"
  ,Su01o :double "Экономия за результаты подр. импорт"
  ,Su02o :double "Экономия фонд руководителя  импорт"
)
with index
( tmp_Fond01 = Podr_Kod+sTypeVed
);
//----------------------------------
var
  nm_file:string;
  logfilenm:string;
  logfilekl:boolean;
  wYear_Flt,wMonth_Flt:word;
  Status              : GP_Status NoAuto;

//----------------------------------
Create view
as select GP_KPI_DOC.Nrec
From GP_KPI_DOC,GP_KPI_DOC_SP
 ,tmp_Fond(tmp_Fond01)
 ,GP_KPI_FOND
 ,KatPodr
 ,KatPodr KatPodr_SP
 ,GP_TypeDocs
where ((
       2            == GP_KPI_DOC.wType
and wMonth_Flt      == GP_KPI_Doc.wMonth(noindex)
and wYear_Flt       == GP_KPI_Doc.wYear(noindex)
and GP_KPI_DOC.Nrec == GP_KPI_DOC_SP.cGP_KPI_DOC
and GP_KPI_DOC.cKatPodr    == KatPodr.nrec
and GP_KPI_DOC_SP.cKatPodr == KatPodr_SP.nrec
//-----------------------------
and 2 == GP_TypeDocs.wType
))
bounds byErr root == tmp_Fond.Podr_kod
 and (ChkErr(tmp_Fond.podr_kod,tmp_Fond.sTypeVed) )
 ordered by TableOrder

;
#include Excel_Imp.Vpp


//------------------------------------------------
Function ChkErr(_podr_kod:string;_sTypeVed:string) : boolean; Cacheable;
{ #__UNUSED__(_podr_kod,_sTypeVed)
  Result:=
      tmp_fond.Su01 <>tmp_fond.Su01o or tmp_fond.Su02 <>tmp_fond.Su02o
}


Function GetFondNrec(_wType:word;_sTypeVed:string):comp;
var _sName:string;
{ Result:=0;
  _sName:='';
  case _wType of
  1:
  { _sName:=if(_sTypeVed='Рабочие','Фонд результативности Рабочие','Фонд результативности РСС');
  }
  2:
  { _sName:=if(_sTypeVed='Рабочие','Фонд руководителя Рабочие','Фонд руководителя РСС');
  }
  end;
  if _sName='' Exit;
  if Getfirst GP_KPI_FOND where (( _Sname == GP_KPI_FOND.Name(noindex)  ))=tsok Result:=GP_KPI_FOND.Nrec;

} //Function GetFondNrec(_wType:word;_sTypeVed:string):comp;


Procedure Fill_Tmp_Table;
var ii:word;
    _cFond1:comp;
    _cFond2:comp;
    _StypeVed:string;
{
  _cFond1:=0;
  _cFond2:=0;
  _StypeVed:='';
  For(ii:=1;ii<=2;ii++)
  { Timelog('Fill_Tmp_Table ii='+string(ii));
    case ii of
    1: _sTypeVed:='Рабочие';
    2: _sTypeVed:='РСС';
    else continue;
    end;
    _cFond1:=GetFondNrec(1,_sTypeVed);
    _cFond2:=GetFondNrec(2,_sTypeVed);
    SQL Select
     max(CASE WHEN GP_KPI_Doc.cGP_KPI_FOND=:(_cFond1) THEN GP_KPI_DOC.Nrec ELSE #Comp(0) END) cDoc01
    ,max(CASE WHEN GP_KPI_Doc.cGP_KPI_FOND=:(_cFond2) THEN GP_KPI_DOC.Nrec ELSE #Comp(0) END) cDoc02
    ,max(CASE WHEN GP_KPI_Doc.cGP_KPI_FOND=:(_cFond1) THEN GP_KPI_DOC_SP.Nrec ELSE #Comp(0) END) cDoc01SP
    ,max(CASE WHEN GP_KPI_Doc.cGP_KPI_FOND=:(_cFond2) THEN GP_KPI_DOC_SP.Nrec ELSE #Comp(0) END) cDoc22SP
    ,max(CASE WHEN GP_KPI_Doc.cGP_KPI_FOND=:(_cFond1) THEN GP_KPI_DOC.DocNumber ELSE #Comp(0) END) DocNumber01
    ,max(CASE WHEN GP_KPI_Doc.cGP_KPI_FOND=:(_cFond2) THEN GP_KPI_DOC.DocNumber ELSE #Comp(0) END) DocNumber02
    ,:(wMonth_flt) wMonth //:word "Месяц"
    ,:(wYear_flt)  wYear //:word "Год"
    ,:(_sTypeVed)  sTypeVed//:string "Тип ведомости"
    ,max(CASE WHEN GP_KPI_Doc.cGP_KPI_FOND=:(_cFond1) THEN GP_KPI_Doc.Description ELSE '' END)  Description //"Примечание"
    ,KatPodr_SP.Kod Podr_kod //:string[20] "Подр."
    ,KatPodr_Sp.name Podr_name // "Наименование подр."
    ,SUM(CASE WHEN GP_KPI_Doc.cGP_KPI_FOND=:(_cFond1) THEN
              CASE WHEN GP_KPI_Doc_SP.FARRVALUE[01]<>''
              THEN CAST(GP_KPI_Doc_SP.FARRVALUE[01] as Double) ELSE 0.00 END
         ELSE 0.00  END ) Su01 //:double "Экономия за результаты подр."
    ,SUM(CASE WHEN GP_KPI_Doc.cGP_KPI_FOND=:(_cFond2) THEN
              CASE WHEN GP_KPI_Doc_SP.FARRVALUE[01]<>''
              THEN CAST(GP_KPI_Doc_SP.FARRVALUE[01] as Double) ELSE 0.00 END
         ELSE 0.00  END ) Su02 //:double "Экономия фонд руководителя"
    ,0.00 Su01o// :double "Экономия за результаты подр. импорт"
    ,0.00 Su02o //:double "Экономия фонд руководителя  импорт"
    From GP_KPI_DOC
    inner join GP_KPI_DOC_SP on GP_KPI_DOC_SP.cGP_KPI_DOC = GP_KPI_DOC.Nrec
    inner join KATPODR  KATPODR_SP on KATPODR_SP.Nrec = GP_KPI_DOC_SP.cKatPodr
    where  GP_KPI_DOC.wMonth =:(wMonth_Flt)
       and GP_KPI_DOC.wYear  =:(wYear_Flt)
       and GP_KPI_DOC.wType=2
       and (GP_KPI_Doc.cGP_KPI_FOND=:(_cFond2) or GP_KPI_Doc.cGP_KPI_FOND=:(_cFond1) )
    Group by KatPodr_Sp.Kod, KatPodr_Sp.Name
    Into tmp_fond ;
  } //For(ii:=1;ii<=2;ii++)
  ReReadRecord(#Tmp_Fond);
} //Procedure Fill_Tmp_Table;

Function FormDoc:boolean;
{ Result:=False;
  logfilenm:=GetStringParameter('Files','OutputFilesDirectory',0)
                               +'!TimeLog.log';

  if BoundActive(tbbyErr)  SubBounds(tbbyErr);
  ReReadRecord(#TMP_Fond);
  ReScanPanel(#GP_KPI_DOC);
  Delete all TMP_Fond;
  //------------------------------
  Fill_Tmp_Table;
  logfilekl:=true;
  //------------------------------
  var isAllOk:boolean=true;
  if not ReadDataFromExcel(0,nm_file,'Лист1',False,true)
  { message('Ошибка импорта из'+chr(13)+nm_file);
      exit ;
  }
  var wexrow_:longint;
  wexrow_:=0;
  var wkl_err:boolean;
  wkl_err:=false;
  v_imp._loop tmp_excel
  { if not nextvisual then
    { isAllOk:=false;
        break;
    }
    wexrow_++ ;
    if wexrow_=1
    { continue;
    }
    var _Podr_kod:string;
    var _sTypeVed:string;
    _Podr_kod:=LpadCh(v_imp.tmp_excel.dmcell[05],'0',3);
    _sTypeVed:=v_imp.tmp_excel.dmcell[03];
    if _Podr_kod='000' then continue;
    if getfirst tmp_fond where ((
           _Podr_kod == tmp_fond.Podr_kod
      and _sTypeVed  == tmp_fond.sTypeVed))<>tsok
    { clearbuffer(#tmp_fond);
      tmp_fond.wMonth     :=word(v_imp.tmp_excel.dmcell[01]);// "Месяц"
      tmp_fond.wYear      :=word(v_imp.tmp_excel.dmcell[02]);// "Год"
      tmp_fond.sTypeVed   :=v_imp.tmp_excel.dmcell[03];// "Тип ведомости"
      tmp_fond.Description:=v_imp.tmp_excel.dmcell[04];// "Примечание"
      tmp_fond.Podr_Kod   :=_Podr_kod; //v_imp.tmp_excel.dmcell[05];// "Подр."
      tmp_fond.Podr_name  :=v_imp.tmp_excel.dmcell[06];// "Наименование подр."
      // ,Su01 :double "Экономия за результаты подр."
      //  ,Su02 :double "Экономия фонд руководителя"
      tmp_fond.Su01o :=Mydouble(v_imp.tmp_excel.dmcell[07]);//);// "Экономия за результаты подр. импорт"
      tmp_fond.Su02o :=Mydouble(v_imp.tmp_excel.dmcell[08]);//);// "Экономия фонд руководителя  импорт"
      insert current tmp_fond;
    }
    else
    { tmp_fond.Su01o :=Mydouble(v_imp.tmp_excel.dmcell[07]);//);// "Экономия за результаты подр. импорт"
      tmp_fond.Su02o :=Mydouble(v_imp.tmp_excel.dmcell[08]);//);// "Экономия фонд руководителя  импорт"
      update current tmp_fond;
    }
  } //v_imp._loop tmp_excel
  if getfirst tmp_fond=tsok {}
  ReScanPanel(#tmp_fond);
  ClearFuncCache;
  Result:=true;
} //FomdDoc


Function InsDoc(_wType:word; _sum:double):boolean;
var _cPodr:comp;
{ Result:=true;
  _cPodr:=0;
  if Getfirst Katpodr where (( tmp_fond.Podr_kod == Katpodr.kod ))<>tsok exit;
  _cPodr:=Katpodr.Nrec;
  if _cPodr=0 then exit;

  //----------------------------
  clearbuffer(#GP_KPI_Doc);
  GP_KPI_DOC.wType   :=2;
  GP_KPI_DOC.cKatpodr:=_cPodr;
  GP_KPI_DOC.wYear   :=wYear_Flt;
  GP_KPI_DOC.wMonth  :=wMonth_flt;
  GP_KPI_DOC.DocCreate:=_CurDateTime;
  GP_KPI_DOC.cCreator :=UserId();
  GP_KPI_DOC.cStatus  :=Status.GetDefault;
  GP_KPI_DOC.DocNumber:=' КИС_Импорт';
  GP_KPI_DOC.DocDate  :=Cur_Date;
  GP_KPI_DOC.cGP_KPI_FOND:=GetFondNrec(_wType,tmp_fond.sTypeVed);
  GP_KPI_DOC.Description:=tmp_fond.Description;
  GP_KPI_DOC.Summa:=_sum;
  insert current GP_KPI_DOC;
  //----------------------------
  clearbuffer(#GP_KPI_Doc_SP);
  GP_KPI_Doc_SP.wType      :=GP_KPI_DOC.wType;
  GP_KPI_Doc_SP.cGP_KPI_Doc:=GP_KPI_DOC.Nrec ;
  GP_KPI_Doc_SP.cKatpodr   :=_cPodr;
  GP_KPI_Doc_SP.FARRVALUE[01]:=string(_sum,0,2);
  Insert current GP_KPI_Doc_SP;
} //Function InsDoc(_wType:word; _sum:double):boolean;

Procedure MakeDoc;
{ if not BoundActive(tbbyErr) AddBounds(tbbyErr);
   StartNewVisual(vtNumericVisual, vfTimer, 'Ждите, идет формирование премий', 0);
   ReReadRecord(#tmp_fond);
   _Loop tmp_fond
   { if not NextVisual break;
     if not ChkErr(tmp_fond.podr_kod,tmp_fond.sTypeVed) continue;
     if tmp_fond.su01<>tmp_fond.su01o
     if tmp_fond.su01o>0
     { if tmp_fond.cDoc01>0
       { if getfirst GP_KPI_DOC    where (( tmp_fond.cDoc01   == GP_KPI_DOC.Nrec    ))<>tsok continue;
         if getfirst GP_KPI_DOC_SP where (( tmp_fond.cDoc01SP == GP_KPI_DOC_SP.Nrec ))<>tsok continue;
         continue;// Не будем пока менять
       }
       else InsDoc(1,tmp_fond.su01o);
     }
     if tmp_fond.su02<>tmp_fond.su02o
     if tmp_fond.su02o>0
     { if tmp_fond.cDoc02>0
       { if getfirst GP_KPI_DOC    where (( tmp_fond.cDoc02   == GP_KPI_DOC.Nrec    ))<>tsok continue;
         if getfirst GP_KPI_DOC_SP where (( tmp_fond.cDoc02SP == GP_KPI_DOC_SP.Nrec ))<>tsok continue;
         continue;// Не будем пока менять
       }
       else InsDoc(2,tmp_fond.su02o);
     }
   } //_Loop tmp_prem
   if getfirst tmp_fond=tsok {}
   ReScanPanel(#tmp_fond);
   ReScanPanel(#GP_KPI_DOC);
   StopVisual('',0);

} //Procedure MakeDoc;


//====================================================
Window wnGetFond 'Результаты' (,,sci1ESC);
//====================================================
Screen scrRes ;
Show at (,,,2);
Table GP_KPI_DOC;

Fields
 [Err]  if(BoundActive(tbbyErr),'Только отклонения','') : Protect, NoDel;
Buttons
  cmValue1    ,,, 'Установить фильтр', hcAllBOk, scGalDial;
  cmValue2    ,,, 'Только ошибки', hcAllBOk, scGalDial;
//  cmCancel,,, 'Отмена', hcAllBCancel, scGalDial;
<<
 <.Сформировать разницу.>   <.Только ошибки/Все .> .@@@@@@@@@@@@@@@@@@@

>>
end;
Browse brRes (,,sci1ESC);
Show at (,3,,);
Table tmp_fond;
fixedLeft (tmp_fond.podr_kod,tmp_fond.sTypeVed) ;
Fields
  {Font={Color=if(ChkErr(tmp_Fond.podr_kod,tmp_Fond.sTypeVed),ColorsysRed,ColorSysBlack)}};
  tmp_fond.DocNumber01 'Номер','док-та 1':[10],Protect;
  tmp_fond.DocNumber02 'Номер','док-та 2':[10],Protect;
  tmp_fond.wMonth      'Месяц' :[5],Protect;
  tmp_fond.wYear       'Год'   :[5],Protect;
  tmp_fond.sTypeVed    'Тип','ведомости' :[10],Protect;
  tmp_fond.Description 'Примечание' :[10],Protect;
  tmp_fond.Podr_Kod    'Подр.'      :[10],Protect;
  tmp_fond.Podr_name   'Наименование подр.' :[10],Protect;
  tmp_fond.Su01        'Экономия за','результаты подр.':[10,2],Protect;
  tmp_fond.Su01o       'КИС' :[10,2],Protect;
  tmp_fond.Su02        'Экономия фонд','руководителя' :[10,2],Protect;
  tmp_fond.Su02o       'КИС' :[10],Protect;

end;
HandleEvent
cmValue1:
{ MakeDoc;

}
cmValue2:
{ if BoundActive(tbbyErr)  SubBounds(tbbyErr)
  else                     AddBounds(tbbyErr);
  ReReadRecord(#tmp_Fond);
  ReScanPanel(#tmp_Fond);
  ReScanPanel(#GP_KPI_DOC);

}
end;
end;//Window wnGetFond


Screen scrParam;
Table GP_KPI_DOC;
Fields
  wYear_flt:NoProtect;
  wMonth_Flt:NoProtect;
  nm_file:NoProtect;
Buttons
  cmOk    ,,, 'Установить фильтр', hcAllBOk, scGalDial;
  cmCancel,,, 'Отмена', hcAllBCancel, scGalDial;

<<
  Год    .@@@@@@@@@@@@@@@@@@
  Месяц  .@@@@@@@@@@@@@@@@@@
  Файл   .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

 <. Продолжить .>  <.   Отмена   .>
>>
end;


HandleEvent
  cmInit:
  { nm_file:='d:\Galaxy\RedMine\_GP\KPI\Sverka\Фонды.xlsx' ;
    logfilenm:=GetStringParameter('Files','OutputFilesDirectory',0)
                               +'!TimeLog.log';
    timelog_init;
    if getfirst GP_TypeDocs=tsok {}
    Status:=New(GP_Status,InitForTypeDoc(GP_TypeDocs.Name));
    wMonth_Flt:=MonthRas;
    wYear_Flt :=YearRas;
  }
  cmOk:
  { StartNewVisual(vtNumericVisual, vfTimer, 'Ждите, идет сбор данных', 0);
    FormDoc;
    StopVisual('',0);
    RunWindowModal(wnGetFond);
  }
  end;
end.
