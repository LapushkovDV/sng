/*************************************************************************************************\

* Наименование: Ведение телефонного справочника                                                   *
* Контур/Модуль: абстракция интеграция                                                            *
* Примечание:                                                                                     *
*                                                                                                 *
* Вид работы  |Номер         |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *

\*************************************************************************************************/

#include Query.vih
#include QRY_OUT.vih
#include odecl.vih
#include StrFunc.vih
//************************************************************
const
  coMaxElemets = 256;
end;

type String_xMaxEl  = Array [1..coMaxElemets] of string ;
Table Struct Tmp_Alg
(
   Npp  : word        "Порядковый номер"
 , Key  : string[100] "Ключ"
 , Prim : String      "Использование"
)
With Index
( Tmp_Alg01 = Npp
 ,Tmp_Alg02 = Key
);

table struct tCheckFields
(
     nrec : comp
   , FieldNam     : string
   , FieldSynonim : string
   , FieldJSON    : string
   , cLevel       : comp
)
with index
(
 ind0 = nrec
)
;


table struct tAbstrResultValues
(
     nrec     : comp
   , value    : String_xMaxEl
)
with index
(
   ind0 = nrec (unique, surrogate)
)
;
table struct tForExportJSON
(
     nrec    : comp
   , cParent : comp
   , cLVL    : comp
   , value   : String_xMaxEl
)
with index
(
   ind0 = nrec (unique, surrogate)
 , ind1 = cParent + cLVL
// , ind2 = cLVL
)
;
Interface QRY_OUT 'Генерация запросов и файлов на основе шаблонов и инстансов';


var arrAbstrResultFldNames : array [1..1] of string;
var arrFldFormulas         : array [1..1] of string;
Function Fill_Tmp_alg : boolean; forward;
create view
var
  m_service : TPTR;
  odecl : declension ; // склонение фио
  _iStrFunc : StringFunctions; // Функции работы со строками
//  JSONObj_ArrayOneElement : tptr;
//  JSONObj_Array : tptr;
  JSONObj : TPtr;
as select
  tCheckFields_1.FieldNam
from
   tCheckFields tCheckFields_1
 , tCheckFields tCheckFields_2
 , tForExportJSON
 , tForExportJSON tForExportJSON_1
 , Tmp_Alg
 , tAbstrResultValues
 , QRY_Instance_FLD_LVL QRY_INST_FLD_LVL_LOOP
 , QRY_Instance_FLD_LVL QRY_INST_FLD_LVL_LOOP_1
 , QRY_Instance_FLD_LVL QRY_INST_FLD_LVL_LOOP_2
 , QRY_Instance_FLD QRY_INST_FLD_LOOP
 , Persons Persons_DECL
 , catalogs cat_search

;

window winTmp_Alg 'Выбор алгоритма', cyan;
browse brTmp_Alg;
 table Tmp_Alg;
  Fields
   Tmp_Alg.Npp  '№','п/п'            : [1], Protect, NoPickButton;
   Tmp_Alg.Key  'Ключ','формула'     : [6], Protect, NoPickButton;
   Tmp_Alg.Prim 'Описание','формулы' : [12], Protect, NoPickButton;
end;
end;
windowevent winTmp_Alg ;
 cminit:{
  Fill_Tmp_alg;
 }
 cmdefault: {
   closewindowex(winTmp_Alg, cmDefault)
 }
end;
function GetFormula : string; {
 result := '';
 if runwindowmodal(winTmp_Alg) = cmDefault {
   result := Tmp_Alg.Key;
 }
}



function TestQueryTemplate(_fcQRY_TMPLT : comp; var _err : string): boolean; {
   if BoundActive(tbbyQRY_TMPLTNrec) {
     popbounds(tbbyQRY_TMPLTNrec);
   }
  set _cQRY_TMPLT := _fcQRY_TMPLT;
  pushbounds(tbbyQRY_TMPLTNrec);

 if getfirst QRY_TMPLT <> tsOK {
   _err := 'Не найден шаблон запроса ' + string(_fcQRY_TMPLT ,0,0);
   result := false;
 }

  var objQuery: iQuery;
  var _qryID   : tPtR;
  var oResultSet : IResultSet;

  sqlFreeStr(_qryID);
  _qryID := 0;

  MysqlAddStr(_qryID, ' select top 1 1                                    ');

  MysqlAddStr(_qryID, ' from '+GetTableNameByCode(QRY_TMPLT.TABLECODE));
  _loop QRY_TMPLTSP {
      MysqlAddStr(_qryID, QRY_TMPLTSP.join_type + ' ' +TblTMPLTSP.XF$NAME + ' ' + QRY_TMPLTSP.SynonimName + ' on ' + QRY_TMPLTSP.JoinTerms);
    }
  MysqlAddStr(_qryID, ' where');

   MemoFile.OpenMemo(QRY_TMPLT.WhereTerms);
   do {
      var StringValue1 : string = '';
      MemoFile.readln(StringValue1);
      MysqlAddStr(_qryID, StringValue1);
    } while (not MemoFile.EOF);
   MemoFile.Close;

  objQuery := queryManager.createQuery(_qryID);
  if (objQuery.execute.errorCode != tsOk) then {
    _err := 'Ошибка проверки записей'+ objQuery.ErrorCode;
    mylog(_err);
    result := false;
    sqlFreeStr(_qryID);
    Exit;
  }
  else{
    result := true;
  }
}
function checkFieldsDuplicate(var _err: string) : boolean; {
  delete all tCheckFields_1;
  _loop QRY_INST_FLD {
    insert tCheckFields_1 set
       tCheckFields_1.nrec         := QRY_INST_FLD.nrec
     , tCheckFields_1.FieldNam    := QRY_INST_FLD.FieldName
     , tCheckFields_1.FieldSynonim := QRY_INST_FLD.FieldSynonim
     , tCheckFields_1.FieldJSON    := QRY_INST_FLD.FieldJSON
     , tCheckFields_1.cLevel       := QRY_INST_FLD.cLevel
    ;
  }

  _Loop tCheckFields_1 {
      var _prohstr : string = '';
      if trim(tCheckFields_1.cLevel) = 0h {
          _err := 'Не указан уровень вложенности поля JSON "'+tCheckFields_1.FieldNam+'"';
          result := false;
          exit;
        }

      if trim(tCheckFields_1.FieldJSON) = '' {
          _err := 'Не указано наименование поля JSON "'+tCheckFields_1.FieldNam+'"';
          result := false;
          exit;
        }

      if(getfirst tCheckFields_2 where
                              tCheckFields_2.nrec         <> tCheckFields_1.nrec
                          and tCheckFields_2.cLevel      = tCheckFields_1.cLevel
                          and trim(tCheckFields_2.FieldJSON) = trim(tCheckFields_1.FieldJSON)
        ) = tsok {
          _err := 'Дубль наименование поля JSON "'+tCheckFields_1.FieldJSON+'" на одном уровне вложенности "'+
              if(getfirst QRY_INST_FLD_LVL_LOOP_1 where ((tCheckFields_1.cLevel == QRY_INST_FLD_LVL_LOOP_1.nrec)) = tsOK,QRY_INST_FLD_LVL_LOOP_1.fld_json_name,'НЕ ВЫБРАН')+'"';
          result := false;
          exit;
        }

      if IsStrHaveProhibitSymbols(trim(tCheckFields_1.FieldSynonim), _prohstr) {
        _err := 'Поле "'+tCheckFields_1.FieldNam+'" синоним "'+ trim(tCheckFields_1.FieldSynonim)+'" содержит запрещенный символ "'+_prohstr+'"';
        result := false;
        exit;
      }

      if trim(tCheckFields_1.FieldNam) = '' {
          _err := 'Есть пустое поле в запросе nrec = ' + string(tCheckFields_1.nrec,0,0);
          result := false;
          exit;
        }


      if trim(tCheckFields_1.FieldSynonim) = '' {
          _err := 'Не указан синоним поля "'+tCheckFields_1.FieldNam+'"';
          result := false;
          exit;
        }

      if(getfirst tCheckFields_2 where
                              tCheckFields_2.nrec         <> tCheckFields_1.nrec
                          and trim(tCheckFields_2.FieldSynonim) = trim(tCheckFields_1.FieldSynonim)
        ) = tsok {
          _err := 'Дубль синонима поля "'+tCheckFields_2.FieldSynonim+'"';
          result := false;
          exit;
        }

  }
  result := True;
}

function getinsertUniqValuesByLVL(_cLevel : comp; _arrOneValue : String_xMaxEl; _cParentJSON : comp): comp; {
 result := 0h;
  var _arrNumsOfValue : array[1..1] of word;
    _loop QRY_INST_FLD_LOOP where(( _cQRY_INST == QRY_INST_FLD_LOOP.cInstance
                                and _cLevel == QRY_INST_FLD_LOOP.cLevel)) {
        var _i : word = 0;
        for (_i := 1; _i<= getlimit(arrAbstrResultFldNames); _i++) {
          if arrAbstrResultFldNames[_i] = QRY_INST_FLD_LOOP.FieldSynonim
           then {
             _arrNumsOfValue[getlimit(_arrNumsOfValue)] := _i;
             setlimit(_arrNumsOfValue,getlimit(_arrNumsOfValue)+1);
           }
        }
      }
    setlimit(_arrNumsOfValue,getlimit(_arrNumsOfValue)-1);
    if getlimit(_arrNumsOfValue) = 0 then exit;
    var _isequal : boolean = False;
    var _i : word = 0;
       _loop tForExportJSON where ((_cLevel == tForExportJSON.cLVL
                           and _cParentJSON == tForExportJSON.cParent))
                   and tForExportJSON.value[_arrNumsOfValue[1]] = _arrOneValue[_arrNumsOfValue[1]] // встаем на позицию по первому значению, а потом сравним в строке остальные
         {
          _isequal := True;
          if getlimit(_arrNumsOfValue) > 1 then {
              for (_i := 2; _i<= getlimit(_arrNumsOfValue); _i++) {
                 if tForExportJSON.value[_arrNumsOfValue[_i]] <> _arrOneValue[_arrNumsOfValue[_i]] then { // сравниваем все значения в строке
                    _isequal := false;
                    break;
                 }
               }
           }
           if _isequal then break; // все значения по строке сошлись
         }


    if _isequal = false {
      clearbuffer(#tForExportJSON);
      for (_i := 1; _i<= getlimit(_arrNumsOfValue); _i++) {
         tForExportJSON.value[_arrNumsOfValue[_i]] := _arrOneValue[_arrNumsOfValue[_i]];
        }
       tForExportJSON.cLVL    := _cLevel;
       tForExportJSON.cParent := _cParentJSON;
      insert current tForExportJSON;
    }
  result := tForExportJSON.nrec;
}
procedure insertvaluesbylevel(_fcLevel : comp; _arrOneValue : String_xMaxEl; _cParentJSON : comp);
{
 _cParentJSON := getinsertUniqValuesByLVL(_fcLevel,_arrOneValue,_cParentJSON);

  _loop QRY_INST_FLD_LVL_LOOP where ((_cQRY_INST == QRY_INST_FLD_LVL_LOOP.cInstance
                                     and _fcLevel == QRY_INST_FLD_LVL_LOOP.cLevel (noindex)
                                     )) {
    pushpos(#QRY_INST_FLD_LVL_LOOP);
     insertvaluesbylevel(QRY_INST_FLD_LVL_LOOP.nrec, _arrOneValue, _cParentJSON);
    poppos(#QRY_INST_FLD_LVL_LOOP);
  }
}

function GenerateJSONfromtForExportJSON(_cParentJSON, _cLVL : comp; var _fld_json_name : string) : tptr;
{
//  JSONObj := HttpConnect_JsonObjectCreate(m_service);
  var JSONObj_Array : tptr;
  var JSONObj_ArrayOneElement : tptr;

   var isFound : boolean = false;

   if getfirst QRY_INST_FLD_LVL_LOOP_2 where ((_cLVL == QRY_INST_FLD_LVL_LOOP_2.nrec)) <> tsOK then exit;

  _loop tForExportJSON where ((_cParentJSON  == tForExportJSON.CPARENT and _cLVL == tForExportJSON.cLVL)) {
    if isFound = false {
      JSONObj_Array := HttpConnect_JsonArrayCreate(m_service);
      isFound := true;
     }

   if (HttpConnect_LastError(m_service) <> 0) then mylog('HttpConnect_LastErrorText = ' + HttpConnect_LastErrorText(m_service));
   JSONObj_ArrayOneElement := HttpConnect_JsonObjectCreate(m_service);
    var _arrNumsOfValue : array[1..1] of word;
    _loop QRY_INST_FLD_LOOP where(( _cQRY_INST == QRY_INST_FLD_LOOP.cInstance
                       and tForExportJSON.cLvl == QRY_INST_FLD_LOOP.cLevel)) {
       if (HttpConnect_LastError(m_service) <> 0) then mylog('HttpConnect_LastErrorText = ' + HttpConnect_LastErrorText(m_service));
        var _i : word = 0;
        for (_i := 1; _i<= getlimit(arrAbstrResultFldNames); _i++) {
          if arrAbstrResultFldNames[_i] = QRY_INST_FLD_LOOP.FieldSynonim
           then {
             HttpConnect_JsonObjectAddString(m_service, JSONObj_ArrayOneElement, QRY_INST_FLD_LOOP.FieldJSON, tForExportJSON.value[_i]);
             if (HttpConnect_LastError(m_service) <> 0) then mylog('HttpConnect_LastErrorText = ' + HttpConnect_LastErrorText(m_service));
           }
        }
      }

    if getfirst tForExportJSON_1 where ((tForExportJSON.nrec  == tForExportJSON_1.CPARENT))  = tsOK {

       _loop QRY_INST_FLD_LVL_LOOP where ((_cQRY_INST == QRY_INST_FLD_LVL_LOOP.cInstance)){
            pushpos(#QRY_INST_FLD_LVL_LOOP);
            pushpos(#tForExportJSON);
            var _JSONObj_ArraySUB : tptr = GenerateJSONfromtForExportJSON(tForExportJSON.nrec, QRY_INST_FLD_LVL_LOOP.nrec, _fld_json_name);
            poppos(#tForExportJSON);
            poppos(#QRY_INST_FLD_LVL_LOOP);
            if _fld_json_name <> '' then {
              HttpConnect_JsonObjectAddObject(m_service, JSONObj_ArrayOneElement, _fld_json_name, _JSONObj_ArraySUB);
            }
          }
    }

    HttpConnect_JsonArrayAddObject(m_service, JSONObj_Array, JSONObj_ArrayOneElement);
    if (HttpConnect_LastError(m_service) <> 0) then mylog('HttpConnect_LastErrorText = ' + HttpConnect_LastErrorText(m_service));
    if getfirst QRY_INST_FLD_LVL_LOOP_2 where ((_cLVL == QRY_INST_FLD_LVL_LOOP_2.nrec)) <> tsOK then exit;
    _fld_json_name := QRY_INST_FLD_LVL_LOOP_2.fld_json_name;
    // mylog('_fld_json_name = ' + _fld_json_name)
   }

 result := JSONObj_Array;
}


function generateJSONFromtAbstrResultValues : tptr; {
   _loop tAbstrResultValues {
     var _arrOneValue : String_xMaxEl;
     var _i : word = 0;
     for(_i:=1;_i <= coMaxElemets; _i++){
         _arrOneValue[_i] := tAbstrResultValues.value[_i];
       }
      insertvaluesbylevel(0h, _arrOneValue, 0h);  // заполняем tForExportJSON
    }


   putTableToDBF(#tForExportJSON, GenerateNewFile+'_tForExportJSON.dbf', ptfFullTable);

   var _fld_json_name : string = '';
   var JSONObj_Array : tptr;
   JSONObj := HttpConnect_JsonObjectCreate(m_service);
       _loop QRY_INST_FLD_LVL_LOOP_1 where ((_cQRY_INST == QRY_INST_FLD_LVL_LOOP_1.cInstance)) {
              JSONObj_Array := GenerateJSONfromtForExportJSON(0h, QRY_INST_FLD_LVL_LOOP_1.nrec, _fld_json_name);
              if _fld_json_name <> '' then {
              HttpConnect_JsonObjectAddObject(m_service, JSONObj, _fld_json_name, JSONObj_Array);
            }
          }
   result := JSONObj;
}
Procedure Fill_Tmp_alg_One(_Npp:word; _Key:string; _Prim:string='');
{  Insert into Tmp_Alg set
      Tmp_Alg.Npp :=_Npp
    , Tmp_Alg.Key :=_Key
    , Tmp_Alg.Prim:=_Prim
   ;
}
#doc
  Заполнить справочник алгоритмов
#end
Function Fill_Tmp_alg : boolean;
{ Result:=True;
  //if wType_Alg=_wType_Alg
  // if (getfirst Tmp_Alg)=tsok      Exit;
  // delete Tmp_Alg;
  if (getfirst Tmp_Alg)=tsok
     Exit;
//   Fill_Tmp_alg_One(00,'USERNAME','Имя текущего пользователя');
   Fill_Tmp_alg_One(01,'%DECLENSION(%VALUE%,NCASE)%','Склонение ФИО ,%VALUE%=persons.nrec, NCASE - номер падежа (2-6)');
   Fill_Tmp_alg_One(02,'%UP_CATALOGS_UNDER_CODE(%VALUE%,''0000'')%','Подняться вверх по CATALOGS пока вышестоящая запись не будет иметь код = второй параметр. %VALUE% = CATALOGS.NREC ');
   Fill_Tmp_alg_One(03,'%DIVSTR(%VALUE%)%','Из указанной строки выделяется первое слово, в том числе и с дефисами. %VALUE% = ФИО');
   Fill_Tmp_alg_One(04,'%GET_LASTNAME(%VALUE%)%','Извлечение фамилии из заданной строки ФИО. %VALUE% = ФИО');
   Fill_Tmp_alg_One(05,'%GET_FIRSTNAME(%VALUE%)%','Извлечение имени из заданной строки ФИО. %VALUE% = ФИО');
   Fill_Tmp_alg_One(06,'%GETPATRONYMIC(%VALUE%)%','Извлечение отчества из заданной строки ФИО. %VALUE% = ФИО');
   Fill_Tmp_alg_One(07,'%GETFIOIN(%VALUE%)%','ФИО в формате "Фамилия И.О.". %VALUE% = ФИО');
   Fill_Tmp_alg_One(08,'%GETFIO_ZAR(%VALUE%)%','ФИО в формате, заданном настройкой "Настройки Галактики \ Управление персоналом \ Расчеты с персоналом \ Отчеты \ Вывод ФИО". %VALUE% = ФИО');
   Fill_Tmp_alg_One(09,'%GETRUB(%VALUE%,okrug1)%','Выделяет рубли из суммы = %VALUE%, okrug1 - количество знаков после запятой');
   Fill_Tmp_alg_One(10,'%GETKOP(%VALUE%, okrug1)%','Выделяет копейки из суммы = %VALUE% и округляет с указанной точностью');
   Fill_Tmp_alg_One(11,'%GETSUMRATE(%VALUE%,okrug1)%','Округляет сумму=%VALUE% с указанной точностью и преобразует в строку');
   Fill_Tmp_alg_One(12,'%GETFIODIR%','ФИО из настройки "Право подписи приказов по персоналу" или из настройки "Руководитель"');
} //Function Fill_Tmp_alg(_wType_Alg:word):boolean;

function getpodrunder_nrec(_code : string; _crec , _cParent: comp) : comp; cacheable;{

//  mylog('getpodrunder_nrec _code = ' +_code+' _crec = '+ _crec+ ' _cParent = '+ _cParent);

  if getfirst fastfirstrow cat_search where ((_cParent == cat_search.nrec)) = tsOK
   {
    if cat_search.code = _code {
        if getfirst fastfirstrow cat_search where ((_crec == cat_search.nrec)) = tsOK {
          result := cat_search.nrec;
          exit;
        }
        else {
         result := 0h;
         exit;
        }
      }
      else {
        result := getpodrunder_nrec(_code, cat_search.nrec, cat_search.cParent)
      }
   }
   else {
     result := 0h;
     exit;
   }
}

function getpodrunder(_code : string; _crec , _cParent: comp) : string; cacheable;{
  result := '';
//  mylog('getpodrunder _code = ' +_code+' _crec = '+ _crec+ ' _cParent = '+ _cParent);
  var _crec : comp = getpodrunder_nrec(_code, _crec, _cParent);
//  mylog('getpodrunder _crec = ' +_crec);
   if getfirst cat_search where (( _crec == cat_search.nrec)) = tsOK then result := cat_search.longname ;
}

function ReplaceByValues(Formula:string; _value: string):string;
{
  Fill_Tmp_alg;
//  mylog('function ReplaceByValues Formula = '+ Formula + ' _value = ' +_value );
  Formula := replace(UPCASE(Formula),UPCASE('%value%'),_value);


  result:=UpCase(Formula);
//  mylog('ReplaceByValues Formula = ' + Formula);
//   mylog('function ReplaceByValues  replace(Formula) ='+ Formula);
  _Loop Tmp_Alg ordered by Index Tmp_Alg02
  {
     //нужно вычислять
      var _Key,_SubKey:string;
      var _stop:word=0;
      _SubKey:='';
      _Key:=UpCase(Tmp_Alg.Key);
      if pos('(',_Key)>0
      { _Key:=substr(_Key,1,pos('(',_Key));
        _SubKey:=')%'
      }
//if timelog_Status>2 then timelog('_Key='+_Key+'; _SubKey='+_SubKey);
      do {
        var ii_first,_Len:word;
        ii_first:=pos(_Key,Result);
        var _Parameter:string='';
        _Len:=0;
        if ii_first=0 break;
        //длина подстроки всегда короче на 2 символа
        if _SubKey<>''
        { _Len:=Pos(_SubKey,Substr(Result,ii_first+Length(_Key),255))-1;
          _Parameter:=Substr(Result,ii_first+Length(_Key),_Len);
        }
        else _len:=0;

//        mylog('_Len='+string(_Len)+'; _Parameter='+_Parameter+'; _Key='+_Key+'; Formula='+ Formula);

        var _Res:string;//результат
        _Res:='';
        case _Key of
        //01, %EM_FieldDocbyPers(wtype,Filedname,param)% -  wtype = word,
        // Filedname = string, param - string. Получает значение поля ведомости по типу док-та
        //и наименованию поля: в текущем месяце находится док-нт типа,
        //в нем ищется строка по человеку и в этой строке берется значение поля
        //FieldName. Param - дополнительный параметр');
        '%DECLENSION(': {
          /*
          %VALUE% - NREC persons
          1-6 - номер падежа
          */
          var _cPerson : comp = comp(ParseWord(_Parameter,1,','));
          var _NCase   : word = ParseWord(_Parameter,2,',');

          //mylog('_cPerson = ' + _cPerson + ' _NCase = ' + _NCase);

          var _Infinitive : string = '';
          if getfirst fastfirstrow persons_decl where ((_cPerson == persons_decl.nrec)) = tsOK {
            _Infinitive := persons_decl.fio
          }
          _res:=''''+Odecl.FIODeclension(_cPerson, _Infinitive, _NCase)+'''';
          //mylog('_res = ' + _res);
         }
        '%UP_CATALOGS_UNDER_CODE(': {
          _res := ''' ''';
          var _cCatalogs : comp = comp(ParseWord(_Parameter,1,','));
          var _UpperCode : string = ParseWord(_Parameter,2,',');
//          mylog('%UP_CATALOGS_UNDER_CODE( _cCatalogs = ' + _cCatalogs + ' _UpperCode = '  + _UpperCode);
          if getfirst fastfirstrow cat_search where ((_cCatalogs == cat_search.nrec)) = tsOK {
             _res := ''''+getpodrunder(_UpperCode, cat_search.nrec, cat_search.cParent)+'''';
//             mylog('_res =' + _res);
           }
         }
        '%DIVSTR(': { // Из указанной строки выделяется первое слово, в том числе и с дефисами
                var _Fio : string = ParseWord(_Parameter,1,',');
               _res := ''''+_iStrFunc.DivStr(_Fio)+'''';
         }
        '%GET_LASTNAME(':{ // Извлечение фамилии из заданной строки ФИО
                var _Fio : string = ParseWord(_Parameter,1,',');
               _res := ''''+_iStrFunc.Get_LastName(_Fio)+'''';
         }
        '%GET_FIRSTNAME(':{ // Извлечение имени из заданной строки ФИО
                var _Fio : string = ParseWord(_Parameter,1,',');
               _res := ''''+_iStrFunc.Get_FirstName(_Fio)+'''';
         }
        '%GETPATRONYMIC(':{ // Извлечение отчества из заданной строки ФИО
                var _Fio : string = ParseWord(_Parameter,1,',');
               _res := ''''+_iStrFunc.GetPatronymic(_Fio)+'''';
         }
        '%GETFIOIN(':{ // ФИО в формате "Фамилия И.О."
                var _Fio : string = ParseWord(_Parameter,1,',');
                if _iStrFunc.GetFioIn(_Fio) then {
                 _res := ''''+_Fio+'''';
                }
         }
        '%GETFIO_ZAR(':{ // ФИО в формате, заданном настройкой "Настройки Галактики \ Управление персоналом \ Расчеты с персоналом \ Отчеты \ Вывод ФИО"
                var _Fio : string = ParseWord(_Parameter,1,',');
               _res := ''''+_iStrFunc.GetFIO_ZAR(_Fio)+'''';
         }
        '%GETRUB(':{ // Выделяет рубли из суммы
                var _Sum : double = ParseWord(_Parameter,1,',');
                var _okrug1 : double = ParseWord(_Parameter,2,',');
               _res := ''''+_iStrFunc.GetRub(_Sum, _okrug1)+'''';
         }
        '%GETKOP(':{ // Выделяет копейки из суммы и округляет с указанной точностью
                var _Sum : double = ParseWord(_Parameter,1,',');
                var _okrug1 : double = ParseWord(_Parameter,2,',');
               _res := ''''+_iStrFunc.GetKop(_Sum, _okrug1)+'''';
         }
        '%GETSUMRATE(':{ // Округляет сумму с указанной точностью и преобразует в строку
                var _Sum : double = ParseWord(_Parameter,1,',');
                var _okrug1 : double = ParseWord(_Parameter,2,',');
               _res := ''''+_iStrFunc.GetSumRate(_Sum, _okrug1)+'''';
         }
        end;
//        mylog('_Res='+_Res);
        Result:=Substr(Result,1,ii_first-1)
               +_Res
               +Substr(Result,ii_first+Length(_Key)+if(_len=0,0,_Len+2),255);
        //Timelog('Result='+Result);
        //подстрахуемся, если что то пойдет не так
        _stop++;
        if _stop>0 break;
        //Break;
      } while True;
  } //_Loop Tmp_Alg ordered by Imdex Tmp_Alg02
  //mylog('ReplaceByValues = ' +Result);
} //function ReplaceByValues(Formula:string):string;

function ApplyFormulaToValue(_value, _Formula: string) : string; {
  //mylog('function ApplyFormulaToValue _value ='+_value+' _Formula = ' + _Formula);
  result := _value;
  if trim(_Formula)<> '' {
    var _error : string;
//    mylog('ApplyFormulaToValue _Formula = '+ _Formula);
    var _ReplaceByValues : string = ReplaceByValues(_Formula, _value );
//    mylog('ApplyFormulaToValue _ReplaceByValues = '+ _ReplaceByValues);
    result := LotExpressionFunc(_ReplaceByValues, _error);
    if trim(_error) <> '' then mylog('LotExpressionFunc Ошибка вычисления по формуле "'+_ReplaceByValues+'". Ошибка: '+_error);
   }
}
function generateJSONByResultSet(_fcQRY_INST : comp; _iRes : iResultSet; var _err: string): boolean; {
   if BoundActive(tbbyQRY_INSTNrec) {
       popbounds(tbbyQRY_INSTNrec);
   }
   set _cQRY_INST := _fcQRY_INST;
    pushbounds(tbbyQRY_INSTNrec);

   if NullVipRef(_iRes) then {
     _err := 'ОШИБКА ВЫПОЛНЕНИЯ ЗАПРОСА В БД';
     mylog(_err);
     result := false;
     exit;
    }

   if _iRes.getFirst <>  tsOk {
     _err := 'В результате запроса нет данных';
     mylog(_err);
     result := false;
     exit;
    }


  m_service := HttpConnect_CreateEx('http://ya.ru', '', '', true);


    SetLimit(arrAbstrResultFldNames,0);
    SetLimit(arrAbstrResultFldNames,1);
    SetLimit(arrFldFormulas,0);
    SetLimit(arrFldFormulas,1);

   _loop QRY_INST_FLD {
      arrAbstrResultFldNames[getlimit(arrAbstrResultFldNames)] := QRY_INST_FLD.FieldSynonim;
      arrFldFormulas[getlimit(arrFldFormulas)] := QRY_INST_FLD.PostFunction;
      SetLimit(arrAbstrResultFldNames, GetLimit(arrAbstrResultFldNames) + 1);
      SetLimit(arrFldFormulas, GetLimit(arrFldFormulas) + 1);
    }
   SetLimit(arrAbstrResultFldNames, GetLimit(arrAbstrResultFldNames) - 1);
   SetLimit(arrFldFormulas, GetLimit(arrFldFormulas) - 1);

  delete all tAbstrResultValues;
    do {
        var _i : word = 1;
        clearbuffer(tntAbstrResultValues);
        for (_i := 1; _i<= getlimit(arrAbstrResultFldNames); _i++) {
           tAbstrResultValues.value[_i] := ApplyFormulaToValue(_iRes.row.val(arrAbstrResultFldNames[_i]),arrFldFormulas[_i]);
         }
        insert current tAbstrResultValues;
     } while _iRes.getNext = tsOk;

   var JSONObj_Array : tptr = 0;
    JSONObj_Array := generateJSONFromtAbstrResultValues;

   var JSONSJSON     : TPtr    = HttpConnect_JSONObjectToJSON(m_service,JSONObj_Array);
   HttpConnect_SaveToFileEx(m_service, JSONSJSON, GenerateNewFile+'.json', cpUTF8, false);
   HttpConnect_Free(m_service) ;
   result := true;
}

function replaceMacro(_FieldName : string): string; {
   result := replace(Upcase(_FieldName),UPCASE('%USERNAME%'),UserName)
}
function RunQueryInstance(_fcQRY_INST : comp; var _err : string): boolean;{

 if BoundActive(tbbyQRY_INSTNrec) {
     popbounds(tbbyQRY_INSTNrec);
 }
 set _cQRY_INST := _fcQRY_INST;
  pushbounds(tbbyQRY_INSTNrec);

 setFieldSynonims;

 if not checkFieldsDuplicate(_err) {
   mylog(_err);
   result := false;
   exit;
 }

 if getfirst QRY_INST <> tsOK {
   _err := 'Не найден запрос ';
   mylog(_err);
   result := false;
   exit;
 }
  if getfirst QRY_INST_FLD <> tsOK {
   _err := 'Не найдены поля запроса ';
   mylog(_err);
   result := false;
   exit;
  }


 if getfirst QRY_INST_TMPLT <> tsOK {
   _err := 'Не найден шаблон в запросе';
   mylog(_err);
   result := false;
   exit;
 }

  var objQuery: iQuery;
  var _qryID   : tPtR;
  var oResultSet : IResultSet;

  sqlFreeStr(_qryID);
  _qryID := 0;

  MysqlAddStr(_qryID, ' select ');
   var _isFirstStep : boolean = True;
  _loop QRY_INST_FLD {
     MysqlAddStr(_qryID, if(_isFirstStep,'',',')+replaceMacro(QRY_INST_FLD.FieldName)  + if(trim(QRY_INST_FLD.FieldSynonim) <> '',' as "'  +trim(QRY_INST_FLD.FieldSynonim)+'"',''));
    _isFirstStep := false;
  }


  MysqlAddStr(_qryID, ' from '+GetTableNameByCode(QRY_INST_TMPLT.TABLECODE));
  _loop QRY_INST_TMPLTSP {
      MysqlAddStr(_qryID, QRY_INST_TMPLTSP.join_type + ' ' +TblINST_TMPLTSP.XF$NAME + ' ' + QRY_INST_TMPLTSP.SynonimName + ' on ' + QRY_INST_TMPLTSP.JoinTerms);
    }
  MysqlAddStr(_qryID, ' where');

  //условия в секцию WHERE из шаблона
   MemoFile.OpenMemo(QRY_INST_TMPLT.WhereTerms);
   do {
      var StringValue1 : string = '';
      MemoFile.readln(StringValue1);
      MysqlAddStr(_qryID, StringValue1);
    } while (not MemoFile.EOF);
   MemoFile.Close;

  // добавляем условия в секцию WHERE из экземпляра
   MemoFile.OpenMemo(QRY_INST.AddWhereTerms);
   do {
      var StringValue1 : string = '';
      MemoFile.readln(StringValue1);
      MysqlAddStr(_qryID, StringValue1);
    } while (not MemoFile.EOF);
   MemoFile.Close;

  objQuery := queryManager.createQuery(_qryID);
  if (objQuery.execute.errorCode != tsOk) then {
    _err := 'Ошибка проверки записей в запросе '+ objQuery.ErrorCode;
    mylog(_err);
    sqlFreeStr(_qryID);
    result := false;
    Exit;
  }
  else{
    if not generateJSONByResultSet(_cQRY_INST, objQuery.getResultSet, _err) {
      result := false;
      Exit;
    }
  }
 result := true;
}


handleevent // фейса

end;
end.
